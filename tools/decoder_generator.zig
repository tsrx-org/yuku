//! The one decoder generator. Both `decode.js` and `decode-analyzer.js` come out
//! of `generate()` below, selected by `Mode`; `tools/gen_parser_decoder.zig` and
//! `tools/gen_analyzer_decoder.zig` are entry points that do nothing but pick a
//! mode. They used to be 2,280-line copies of each other, and the copies drifted:
//! 0.1.0 shipped an analyzer decoder that read destructuring element counts out
//! of the wrong half of a packed word, emptying every ArrayPattern/ObjectPattern.
//! Keep the generation logic here so that failure mode stays unreachable.
const std = @import("std");
const parser = @import("parser");
const ast = parser.ast;
const rt = @import("transfer");
const base_decoder = @import("decoder");
const sem_rt = rt.semantic;
const dialect_enabled = if (@hasDecl(parser, "dialect_enabled")) parser.dialect_enabled else false;
const meta = struct {
    pub const BINARY_OPS = [_][]const u8{
        "==", "!=",         "===", "!==", "<", "<=", ">", ">=", "+",  "-",
        "*",  "/",          "%",   "**",  "|", "^",  "&", "<<", ">>", ">>>",
        "in", "instanceof",
    };
    pub const LOGICAL_OPS = [_][]const u8{ "&&", "||", "??" };
    pub const UNARY_OPS = [_][]const u8{ "-", "+", "!", "~", "typeof", "void", "delete" };
    pub const UPDATE_OPS = [_][]const u8{ "++", "--" };
    pub const ASSIGNMENT_OPS = [_][]const u8{
        "=",   "+=",   "-=", "*=", "/=", "%=",  "**=", "<<=",
        ">>=", ">>>=", "|=", "^=", "&=", "||=", "&&=", "??=",
    };
    pub const VAR_KINDS = [_][]const u8{ "var", "let", "const", "using", "await using" };
    pub const PROPERTY_KINDS = [_][]const u8{ "init", "get", "set" };
    pub const METHOD_KINDS = [_][]const u8{ "constructor", "method", "get", "set" };
    pub const FUNCTION_TYPES = [_][]const u8{
        "FunctionDeclaration",
        "FunctionExpression",
        "TSDeclareFunction",
        "TSEmptyBodyFunctionExpression",
    };
    pub const CLASS_TYPES = [_][]const u8{ "ClassDeclaration", "ClassExpression" };
    pub const COMMENT_TYPES = [_][]const u8{ "Line", "Block" };
    pub const SEVERITY = [_][]const u8{ "error", "warning", "hint", "info" };

    // tables with non-string elements, decoder writes them raw and the encoder inverts
    pub const IMPORT_EXPORT_KINDS_RAW = [_][]const u8{ "\"value\"", "\"type\"" };
    pub const ACCESSIBILITY_RAW = [_][]const u8{ "null", "\"public\"", "\"private\"", "\"protected\"" };
    pub const TS_TYPE_OPERATORS_RAW = [_][]const u8{ "\"keyof\"", "\"unique\"", "\"readonly\"" };
    pub const TS_METHOD_SIGNATURE_KINDS_RAW = [_][]const u8{ "\"method\"", "\"get\"", "\"set\"" };
    pub const TS_MODULE_KINDS_RAW = [_][]const u8{ "\"namespace\"", "\"module\"" };
    pub const TS_MAPPED_OPTIONAL_RAW = [_][]const u8{ "false", "true", "\"+\"", "\"-\"" };
    pub const TS_MAPPED_READONLY_RAW = [_][]const u8{ "null", "true", "\"+\"", "\"-\"" };

    pub fn enumTableName(comptime E: type) []const u8 {
        if (E == ast.BinaryOperator) return "BINARY_OPS";
        if (E == ast.LogicalOperator) return "LOGICAL_OPS";
        if (E == ast.UnaryOperator) return "UNARY_OPS";
        if (E == ast.UpdateOperator) return "UPDATE_OPS";
        if (E == ast.AssignmentOperator) return "ASSIGNMENT_OPS";
        if (E == ast.VariableKind) return "VAR_KINDS";
        if (E == ast.PropertyKind) return "PROPERTY_KINDS";
        if (E == ast.MethodDefinitionKind) return "METHOD_KINDS";
        if (E == ast.FunctionType) return "FUNCTION_TYPES";
        if (E == ast.ClassType) return "CLASS_TYPES";
        if (E == ast.ImportOrExportKind) return "IMPORT_EXPORT_KINDS";
        if (E == ast.Accessibility) return "ACCESSIBILITY";
        if (E == ast.TSTypeOperatorKind) return "TS_TYPE_OPERATORS";
        if (E == ast.TSMethodSignatureKind) return "TS_METHOD_SIGNATURE_KINDS";
        if (E == ast.TSModuleDeclarationKind) return "TS_MODULE_KINDS";
        @compileError("no lookup table for enum: " ++ @typeName(E));
    }

    // true when an enum's inverse must be a JS function rather than an object map
    pub fn enumNeedsInverseFn(comptime E: type) bool {
        return E == ast.Accessibility or E == ast.TSMappedTypeModifier;
    }

    // ESTree name overrides where snake to pascal is wrong (ts_jsdoc_ to TSJSDoc)
    const NAME_OVERRIDES = [_]struct { z: []const u8, e: []const u8 }{
        .{ .z = "function_body", .e = "BlockStatement" },
        .{ .z = "binding_rest_element", .e = "RestElement" },
        .{ .z = "object_property", .e = "Property" },
        .{ .z = "identifier_reference", .e = "Identifier" },
        .{ .z = "binding_identifier", .e = "Identifier" },
        .{ .z = "identifier_name", .e = "Identifier" },
        .{ .z = "label_identifier", .e = "Identifier" },
        .{ .z = "ts_bigint_keyword", .e = "TSBigIntKeyword" },
        .{ .z = "ts_jsdoc_nullable_type", .e = "TSJSDocNullableType" },
        .{ .z = "ts_jsdoc_non_nullable_type", .e = "TSJSDocNonNullableType" },
        .{ .z = "ts_jsdoc_unknown_type", .e = "TSJSDocUnknownType" },
    };

    pub fn estreeType(comptime name: []const u8) []const u8 {
        inline for (NAME_OVERRIDES) |o| if (comptime std.mem.eql(u8, name, o.z)) return o.e;
        if (comptime std.mem.startsWith(u8, name, "jsx_")) {
            return "JSX" ++ snakeConvert(name[4..], true);
        }
        if (comptime std.mem.startsWith(u8, name, "ts_")) return "TS" ++ snakeConvert(name[3..], true);
        return snakeConvert(name, true);
    }

    pub fn includeNode(comptime name: []const u8) bool {
        if (comptime std.mem.eql(u8, name, "dialect_node")) return dialect_enabled;
        return true;
    }

    pub fn estreeField(comptime tag: []const u8, comptime field: []const u8) []const u8 {
        if (comptime std.mem.eql(u8, tag, "variable_declaration") and
            std.mem.eql(u8, field, "declarators")) return "declarations";
        // const is a zig keyword so the field is is_const, ESTree renders it as const
        if (comptime std.mem.eql(u8, tag, "ts_enum_declaration") and
            std.mem.eql(u8, field, "is_const")) return "const";
        return snakeConvert(field, false);
    }

    pub fn dialectRoleName(comptime T: type) []const u8 {
        if (T == bool) return "bool";
        if (@typeInfo(T) == .@"struct" and @hasDecl(T, "dialect_role")) {
            return switch (T.dialect_role) {
                .scalar_u32 => "scalar",
                .node_ref => "node",
                .optional_node_ref => "optionalNode",
                .node_list => "nodeList",
                .string_slice => "string",
                .overlay_host => "host",
            };
        }
        @compileError("unsupported dialect field ABI type: " ++ @typeName(T));
    }

    pub fn dialectRecordType(comptime name: []const u8, comptime T: type) []const u8 {
        if (@typeInfo(T) == .@"struct" and @hasDecl(T, "estree_type")) return T.estree_type;
        return estreeType(name);
    }

    pub fn dialectRecordIsOverlay(comptime T: type) bool {
        var hosts: usize = 0;
        for (std.meta.fields(T)) |field| {
            if (std.mem.eql(u8, dialectRoleName(field.type), "host")) hosts += 1;
        }
        if (hosts > 1) @compileError("ambiguous dialect overlay schema in " ++ @typeName(T));
        return hosts == 1;
    }

    pub fn validateDialectSchema() void {
        if (!dialect_enabled) return;
        for (@typeInfo(parser.dialect_schema.Record).@"union".fields, 0..) |record, record_index| {
            for (std.meta.fields(record.type), 0..) |field, i| {
                for (std.meta.fields(record.type)[0..i]) |prior| {
                    if (std.mem.eql(u8, estreeField(record.name, field.name), estreeField(record.name, prior.name)))
                        @compileError("ambiguous duplicate reflected dialect field in " ++ record.name);
                }
            }
            const is_overlay = dialectRecordIsOverlay(record.type);
            for (@typeInfo(parser.dialect_schema.Record).@"union".fields[0..record_index]) |prior| {
                const same_kind = is_overlay == dialectRecordIsOverlay(prior.type);
                if (same_kind and std.mem.eql(u8, dialectRecordType(record.name, record.type), dialectRecordType(prior.name, prior.type)))
                    @compileError("ambiguous reflected dialect ESTree type " ++ dialectRecordType(record.name, record.type));
            }
        }
    }

    // arrays that allow holes, sparse elements become null in ESTree
    pub fn isHoleyArray(comptime tag: []const u8, comptime field: []const u8) bool {
        return std.mem.eql(u8, tag, "array_expression") and std.mem.eql(u8, field, "elements");
    }

    pub fn snakeConvert(comptime name: []const u8, comptime pascal: bool) []const u8 {
        comptime {
            @setEvalBranchQuota(200_000);
            var result: [name.len]u8 = undefined;
            var len: usize = 0;
            var cap = pascal;
            for (name) |c| {
                if (c == '_') {
                    cap = true;
                } else {
                    result[len] = if (cap) std.ascii.toUpper(c) else c;
                    cap = false;
                    len += 1;
                }
            }
            const final = result[0..len].*;
            return &final;
        }
    }

    // identifier dispatch role, ESTree has one Identifier but yuku has five variants
    pub const Role = enum {
        // ESTree Identifier becomes identifier_reference
        auto,
        // binding position becomes binding_identifier, carries decorators optional type in TS
        binding,
        // label slot for break, continue, or a labeled statement
        label,
        // non-binding name slot becomes identifier_name, else the minifier corrupts these names
        name,
    };

    const ROLES = [_]struct { node: []const u8, field: []const u8, role: Role }{
        .{ .node = "variable_declarator", .field = "id", .role = .binding },
        .{ .node = "assignment_pattern", .field = "left", .role = .binding },
        .{ .node = "binding_rest_element", .field = "argument", .role = .binding },
        .{ .node = "binding_property", .field = "value", .role = .binding },
        .{ .node = "catch_clause", .field = "param", .role = .binding },
        .{ .node = "import_specifier", .field = "local", .role = .binding },
        .{ .node = "import_default_specifier", .field = "local", .role = .binding },
        .{ .node = "import_namespace_specifier", .field = "local", .role = .binding },
        .{ .node = "ts_type_parameter", .field = "name", .role = .binding },
        .{ .node = "ts_type_alias_declaration", .field = "id", .role = .binding },
        .{ .node = "ts_interface_declaration", .field = "id", .role = .binding },
        .{ .node = "ts_enum_declaration", .field = "id", .role = .binding },
        .{ .node = "ts_import_equals_declaration", .field = "id", .role = .binding },
        .{ .node = "ts_parameter_property", .field = "parameter", .role = .binding },
        .{ .node = "break_statement", .field = "label", .role = .label },
        .{ .node = "continue_statement", .field = "label", .role = .label },
        .{ .node = "labeled_statement", .field = "label", .role = .label },
        .{ .node = "import_specifier", .field = "imported", .role = .name },
        .{ .node = "import_attribute", .field = "key", .role = .name },
        .{ .node = "export_all_declaration", .field = "exported", .role = .name },
        .{ .node = "export_specifier", .field = "exported", .role = .name },
        .{ .node = "ts_qualified_name", .field = "right", .role = .name },
        .{ .node = "ts_import_type", .field = "qualifier", .role = .name },
    };

    pub fn fieldRole(comptime tag: []const u8, comptime field: []const u8) Role {
        inline for (ROLES) |r| {
            if (comptime std.mem.eql(u8, r.node, tag) and
                std.mem.eql(u8, r.field, field)) return r.role;
        }
        return .auto;
    }

    // encoder special-case set, nodes the generic struct to object mapping can't express
    const SPECIAL = [_][]const u8{
        "formal_parameter",              "formal_parameters",                  "function",
        "arrow_function_expression",     "program",                            "directive",
        "string_literal",                "numeric_literal",                    "bigint_literal",
        "boolean_literal",               "null_literal",                       "regexp_literal",
        "template_element",              "class",                              "method_definition",
        "property_definition",           "unary_expression",                   "binding_property",
        "array_pattern",                 "object_pattern",                     "jsx_text",
        "ts_function_type",              "ts_constructor_type",                "ts_method_signature",
        "ts_call_signature_declaration", "ts_construct_signature_declaration", "ts_mapped_type",
        "ts_module_declaration",         "ts_global_declaration",              "ts_this_parameter",
        "member_expression",             "object_property",                    "ts_property_signature",
        "ts_enum_member",                "ts_index_signature",
    };

    pub fn isSpecial(comptime name: []const u8) bool {
        comptime {
            @setEvalBranchQuota(200_000);
            for (SPECIAL) |s| if (s.len == name.len and std.mem.eql(u8, s, name)) return true;
            return false;
        }
    }
};

const Symbol = parser.traverser.semantic.Symbol;
const Reference = parser.traverser.semantic.Reference;
const ModuleFlags = parser.semantic.module_record.Flags;

const Writer = std.Io.Writer;

pub const Mode = enum { parser, analyzer };

// generates decode.js, the binary AST buffer read back into ESTree. parser mode
// is lean, analyzer mode adds memoized nodes, raw spans and semantic accessors
pub fn generate(w: *Writer, mode: Mode) !void {
    if (comptime !dialect_enabled) {
        return base_decoder.generate(w, @enumFromInt(@intFromEnum(mode)));
    }
    try w.writeAll(
        \\// generated by tools/estree/decoder.zig, do not edit
        \\const NULL = -1;
        \\const _td = new TextDecoder("utf-8", { ignoreBOM: true });
        \\
    );
    try writeLookupTables(w);
    try writeDialectSchema(w);
    if (mode == .analyzer) try writeSemanticConstants(w);
    if (mode == .analyzer) try writeChildTables(w);
    try writeBuildPosMap(w);
    try writeDecodeOpen(w);
    try writeNodeFunction(w, mode);
    try writeDecodeBody(w, mode);
    switch (mode) {
        .parser => try w.writeAll("export { decode };\n"),
        .analyzer => try w.writeAll("export { decode, SymbolFlags };\n"),
    }
}

fn writeDialectSchema(w: *Writer) !void {
    if (comptime !dialect_enabled) return;
    comptime meta.validateDialectSchema();
    try w.writeAll("const DIALECT_TS = Symbol.for(\"yuku.estree.transfer.ts\");\n");
    try w.writeAll("const DIALECT_RECORDS = Object.freeze([\n");
    inline for (@typeInfo(parser.dialect_schema.Record).@"union".fields, 0..) |record, index| {
        try w.print("  Object.freeze({{ tag: {d}, type: \"{s}\", overlay: {}, fields: Object.freeze([", .{
            comptime rt.dialectNodeTag() + 1 + index,
            comptime meta.dialectRecordType(record.name, record.type),
            comptime meta.dialectRecordIsOverlay(record.type),
        });
        inline for (std.meta.fields(record.type), 0..) |field, field_index| {
            if (field_index > 0) try w.writeAll(", ");
            try w.print("Object.freeze({{ name: \"{s}\", role: \"{s}\", slot: {d}, bit: {d} }})", .{
                comptime meta.estreeField(record.name, field.name),
                comptime meta.dialectRoleName(field.type),
                comptime rt.u32SlotForField(record.type, field_index) + rt.NODE_HEADER_U32S,
                comptime rt.flagBitForField(record.type, field_index),
            });
        }
        try w.writeAll("]) }),\n");
    }
    try w.writeAll("]);\n");
}

const symbol_flag_names = [_]struct { zig: []const u8, js: []const u8 }{
    .{ .zig = "function_scoped_var", .js = "FunctionScopedVariable" },
    .{ .zig = "block_scoped_var", .js = "BlockScopedVariable" },
    .{ .zig = "function", .js = "Function" },
    .{ .zig = "class", .js = "Class" },
    .{ .zig = "regular_enum", .js = "RegularEnum" },
    .{ .zig = "const_enum", .js = "ConstEnum" },
    .{ .zig = "value_module", .js = "ValueModule" },
    .{ .zig = "interface", .js = "Interface" },
    .{ .zig = "type_alias", .js = "TypeAlias" },
    .{ .zig = "type_parameter", .js = "TypeParameter" },
    .{ .zig = "namespace_module", .js = "NamespaceModule" },
    .{ .zig = "import", .js = "ValueImport" },
    .{ .zig = "type_import", .js = "TypeImport" },
    .{ .zig = "const_var", .js = "Const" },
    .{ .zig = "ambient", .js = "Ambient" },
    .{ .zig = "parameter", .js = "Parameter" },
    .{ .zig = "catch_var", .js = "CatchVariable" },
    .{ .zig = "exported", .js = "Exported" },
    .{ .zig = "is_default", .js = "Default" },
    .{ .zig = "enum_member", .js = "EnumMember" },
};

fn jsFlagName(comptime zig_name: []const u8) []const u8 {
    inline for (symbol_flag_names) |entry| {
        if (comptime std.mem.eql(u8, entry.zig, zig_name)) return entry.js;
    }
    @compileError("Symbol.Flags field '" ++ zig_name ++
        "' has no js name in symbol_flag_names");
}

fn cell(
    comptime view: []const u8,
    comptime Packed: type,
    comptime field: []const u8,
) []const u8 {
    return std.fmt.comptimePrint("{s}[i * {d} + {d}]", .{
        view, @sizeOf(Packed) / 4, @offsetOf(Packed, field) / 4,
    });
}

fn strCell(
    comptime view: []const u8,
    comptime Packed: type,
    comptime start_field: []const u8,
    comptime end_field: []const u8,
) []const u8 {
    if (@offsetOf(Packed, end_field) != @offsetOf(Packed, start_field) + 4) {
        @compileError("string handle columns must be adjacent: " ++ start_field);
    }
    return std.fmt.comptimePrint("str({s}, {s})", .{
        cell(view, Packed, start_field), cell(view, Packed, end_field),
    });
}

fn writeSemanticConstants(w: *Writer) !void {
    try writeArray(w, "SCOPE_KINDS", &.{
        "global",       "module",      "function",       "block",
        "class",        "staticBlock", "expressionName", "tsModule",
        "functionBody",
    });
    try writeArray(w, "IMPORT_PHASES", &.{ "source", "defer" });
    try writeArray(w, "IMPORT_KINDS", &.{
        "named", "namespace", "sideEffect", "importEquals", "dynamic", "require",
    });
    try writeArray(w, "EXPORT_KINDS", &.{
        "named", "reExport", "namespace", "star", "equals", "global",
    });

    // one entry per Reference.Space value, in enum order, plus the
    // mirrored Space.inTypePosition lookup
    const space_fields = @typeInfo(Reference.Space).@"enum".fields;
    const space_names = comptime blk: {
        var names: [space_fields.len][]const u8 = undefined;
        for (space_fields, 0..) |field, i| names[i] = field.name;
        break :blk names;
    };
    try writeArray(w, "REFERENCE_SPACES", &space_names);
    const space_type_position = comptime blk: {
        var vals: [space_fields.len][]const u8 = undefined;
        for (space_fields, 0..) |field, i| {
            const space = @field(Reference.Space, field.name);
            vals[i] = if (space.inTypePosition()) "true" else "false";
        }
        break :blk vals;
    };
    try writeArrayRaw(w, "REFERENCE_TYPE_POSITION", &space_type_position);

    try w.writeAll("const SymbolFlags = Object.freeze({\n");
    inline for (@typeInfo(Symbol.Flags).@"struct".fields) |field| {
        if (comptime std.mem.eql(u8, field.name, "_")) continue;
        try w.print("  {s}: 1 << {d},\n", .{
            comptime jsFlagName(field.name),
            @bitOffsetOf(Symbol.Flags, field.name),
        });
    }
    try w.print("  Variable: {d},\n", .{@as(u32, @bitCast(Symbol.variable))});
    try w.print("  Import: {d},\n", .{@as(u32, @bitCast(Symbol.any_import))});
    try w.print("  ValueSpace: {d},\n", .{@as(u32, @bitCast(Symbol.value_space))});
    try w.print("  TypeSpace: {d},\n", .{@as(u32, @bitCast(Symbol.type_space))});
    try w.writeAll("});\n");
}

fn writeBuildPosMap(w: *Writer) !void {
    try w.writeAll(
        \\function buildPosMap(src, byteLen, startByte) {
        \\  const m = new Uint32Array(byteLen - startByte + 1);
        \\  const len = src.length;
        \\  let bp = 0, u16p = startByte, i = startByte;
        \\  while (i < len) {
        \\    if (i + 16 <= len) {
        \\      let allAscii = true;
        \\      for (let k = 0; k < 16; k++) {
        \\        if (src.charCodeAt(i + k) >= 0x80) { allAscii = false; break; }
        \\      }
        \\      if (allAscii) {
        \\        for (let k = 0; k < 16; k++) m[bp + k] = u16p + k;
        \\        bp += 16; u16p += 16; i += 16;
        \\        continue;
        \\      }
        \\    }
        \\    const cu = src.charCodeAt(i);
        \\    m[bp] = u16p;
        \\    if (cu < 0x80) { bp++; u16p++; i++; }
        \\    else if (cu < 0x800) { m[bp + 1] = u16p + 1; bp += 2; u16p++; i++; }
        \\    else if (cu < 0xD800 || cu >= 0xE000) {
        \\      m[bp + 1] = u16p + 1; m[bp + 2] = u16p + 1;
        \\      bp += 3; u16p++; i++;
        \\    }
        \\    else {
        \\      m[bp + 1] = u16p + 1; m[bp + 2] = u16p + 2; m[bp + 3] = u16p + 2;
        \\      bp += 4; u16p += 2; i += 2;
        \\    }
        \\  }
        \\  m[byteLen - startByte] = u16p;
        \\  return m;
        \\}
        \\
    );
}

fn writeDecodeOpen(w: *Writer) !void {
    try w.print(
        \\function decode(buffer, source) {{
        \\  const _u8 = new Uint8Array(buffer);
        \\  const aLen = (buffer.byteLength >> 2) << 2;
        \\  const _u32 = new Int32Array(buffer, 0, aLen >> 2);
        \\  const _src = source;
        \\  const _srcLen = _u32[{[u_src]d}];
        \\  const nodeCount = _u32[{[u_nc]d}],
        \\        extraCount = _u32[{[u_ec]d}],
        \\        spLen = _u32[{[u_sp]d}];
        \\  const commentCount = _u32[{[u_cc]d}],
        \\        diagCount = _u32[{[u_dc]d}],
        \\        progIdx = _u32[{[u_pi]d}];
        \\  const attachedCommentCount = _u32[{[u_acc]d}];
        \\  const _flags = _u32[{[u_fl]d}];
        \\  const _isTs = !!(_flags & {[ts]d});
        \\  const _attached = !!(_flags & {[ac]d});
        \\  const _firstNa = _srcLen === 0 ? _src.length : _u32[{[u_fna]d}];
        \\  const _nodesOff = {[hdr]d};
        \\  const eOff = _nodesOff + nodeCount * {[size]d};
        \\  const _extraBase = eOff >> 2;
        \\  const _spOff = eOff + extraCount * 4;
        \\  const dv = new DataView(buffer);
        \\  const _aoOff = _spOff + ((spLen + 3) & ~3);
        \\  const _acOff = _attached ? _aoOff + (nodeCount + 1) * 4 : _aoOff;
        \\  const _cOff = _acOff + attachedCommentCount * {[acsize]d};
        \\  function _poolDecode(s, e) {{
        \\    const a = _spOff + s - _srcLen, b = _spOff + e - _srcLen;
        \\    let hasEd = false;
        \\    for (let i = a; i < b; i++) if (_u8[i] === 0xED) {{ hasEd = true; break; }}
        \\    if (!hasEd) return _td.decode(_u8.subarray(a, b));
        \\    let r = "";
        \\    for (let i = a; i < b; ) {{
        \\      const c = _u8[i];
        \\      if (c < 0x80) {{ r += String.fromCharCode(c); i++; }}
        \\      else if (c < 0xE0) {{
        \\        r += String.fromCharCode(((c & 0x1F) << 6) | (_u8[i+1] & 0x3F));
        \\        i += 2;
        \\      }}
        \\      else if (c < 0xF0) {{
        \\        r += String.fromCharCode(
        \\          ((c & 0x0F) << 12) | ((_u8[i+1] & 0x3F) << 6) | (_u8[i+2] & 0x3F)
        \\        );
        \\        i += 3;
        \\      }}
        \\      else {{
        \\        r += String.fromCodePoint(
        \\          ((c & 0x07) << 18) | ((_u8[i+1] & 0x3F) << 12) |
        \\            ((_u8[i+2] & 0x3F) << 6) | (_u8[i+3] & 0x3F)
        \\        );
        \\        i += 4;
        \\      }}
        \\    }}
        \\    return r;
        \\  }}
        \\  const pm = _firstNa < _srcLen ? buildPosMap(_src, _srcLen, _firstNa) : null;
        \\  const _p = v => !pm || v <= _firstNa ? v : pm[v - _firstNa];
        \\  const str = (s, e) => {{
        \\    if (s === e) return "";
        \\    if (s >= _srcLen) return _poolDecode(s, e);
        \\    if (e <= _firstNa) return _src.slice(s, e);
        \\    return _src.slice(s < _firstNa ? s : pm[s - _firstNa], pm[e - _firstNa]);
        \\  }};
        \\  function nodeArr(s, len) {{
        \\    const r = [];
        \\    const base = _extraBase + s;
        \\    for (let j = 0; j < len; j++) r.push(node(_u32[base + j]));
        \\    return r;
        \\  }}
        \\  function nodeArrHoles(s, len) {{
        \\    const r = [];
        \\    const base = _extraBase + s;
        \\    for (let j = 0; j < len; j++) {{
        \\      const x = _u32[base + j];
        \\      r.push(x !== NULL ? node(x) : null);
        \\    }}
        \\    return r;
        \\  }}
        \\  function fnParams(idx) {{
        \\    const po = _nodesOff + idx * {[size]d};
        \\    const len = _u8[po + {[f0]d}] | (_u8[po + {[f01]d}] << 8);
        \\    const pb = po >> 2;
        \\    const iStart = _u32[pb + {[items]d}], rest = _u32[pb + {[rest]d}];
        \\    const p = [];
        \\    for (let j = 0; j < len; j++) p.push(node(_u32[_extraBase + iStart + j]));
        \\    if (rest !== NULL) p.push(node(rest));
        \\    return p;
        \\  }}
        \\
    , .{
        .u_src = rt.HDR_SOURCE_LEN_U32,
        .u_nc = rt.HDR_NODE_COUNT_U32,
        .u_ec = rt.HDR_EXTRA_COUNT_U32,
        .u_sp = rt.HDR_STRING_POOL_LEN_U32,
        .u_cc = rt.HDR_COMMENT_COUNT_U32,
        .u_dc = rt.HDR_DIAG_COUNT_U32,
        .u_pi = rt.HDR_PROGRAM_INDEX_U32,
        .u_acc = rt.HDR_ATTACHED_COMMENT_COUNT_U32,
        .u_fl = rt.HDR_FLAGS_U32,
        .u_fna = rt.HDR_FIRST_NON_ASCII_U32,
        .ts = rt.FLAG_TS,
        .ac = rt.FLAG_ATTACHED_COMMENTS,
        .hdr = rt.HEADER_SIZE,
        .size = rt.NODE_SIZE,
        .acsize = rt.ATTACHED_COMMENT_SIZE,
        .f0 = rt.NODE_FIELD0_OFFSET,
        .f01 = rt.NODE_FIELD0_OFFSET + 1,
        .items = comptime u32IndexOf(ast.FormalParameters, "items"),
        .rest = comptime u32IndexOf(ast.FormalParameters, "rest"),
    });
}

fn u16At(comptime word: []const u8, comptime byte_in_word: u8) []const u8 {
    const shift = byte_in_word * 8;
    if (shift == 16) return word ++ " >>> 16";
    if (shift == 0) return word ++ " & 65535";
    return std.fmt.comptimePrint("({s} >>> {d}) & 65535", .{ word, shift });
}

fn writeNodeFunction(w: *Writer, mode: Mode) !void {
    comptime std.debug.assert(rt.NODE_FLAGS_OFFSET / 4 == 0);
    const flags_expr = comptime u16At("h0", rt.NODE_FLAGS_OFFSET % 4);
    const f0_expr = comptime u16At(
        std.fmt.comptimePrint("_u32[b + {d}]", .{rt.NODE_FIELD0_OFFSET / 4}),
        rt.NODE_FIELD0_OFFSET % 4,
    );
    try w.print(
        \\  function _attachedCommentsOf(a, e) {{
        \\    const out = Array.from({{ length: e - a }});
        \\    for (let j = a; j < e; j++) {{
        \\      const o = _acOff + j * {[acsize]d};
        \\      const cf = _u8[o + {[ac_fl]d}];
        \\      const vs = dv.getUint32(o + {[ac_vs]d}, true),
        \\            ve = dv.getUint32(o + {[ac_ve]d}, true);
        \\      out[j - a] = {{
        \\        type: (cf & 1) ? "Block" : "Line",
        \\        position: ["before", "after", "inside"][(cf >> 1) & 3],
        \\        sameLine: (cf & 8) !== 0,
        \\        value: str(vs, ve),
        \\      }};
        \\    }}
        \\    return out;
        \\  }}
        \\  function nodeWithComments(i) {{
        \\    const r = _decode(i);
        \\    if (r && r.type !== undefined && r.comments === undefined) {{
        \\      const off = _aoOff + i * 4;
        \\      const a = dv.getUint32(off, true), e = dv.getUint32(off + 4, true);
        \\      if (a !== e) r.comments = _attachedCommentsOf(a, e);
        \\    }}
        \\    return r;
        \\  }}
        \\  function _decode(i) {{
        \\    const b = (_nodesOff + i * {[size]d}) >> 2;
        \\    const h0 = _u32[b];
        \\    const tag = h0 & 255;
        \\    const flags = {[fe]s};
        \\    const f0 = {[f0e]s};
        \\    const f1 = _u32[b + {[d0]d}], f2 = _u32[b + {[d1]d}],
        \\          f3 = _u32[b + {[d2]d}], f4 = _u32[b + {[d3]d}],
        \\          f5 = _u32[b + {[d4]d}], f6 = _u32[b + {[d5]d}],
        \\          f7 = _u32[b + {[d6]d}], f8 = _u32[b + {[d7]d}];
        \\    const _ss = _u32[b + {[ss]d}], _se = _u32[b + {[se]d}];
        \\    const start = !pm || _ss <= _firstNa ? _ss : pm[_ss - _firstNa];
        \\    const end = !pm || _se <= _firstNa ? _se : pm[_se - _firstNa];
        \\    switch (tag) {{
        \\
    , .{
        .size = rt.NODE_SIZE,
        .acsize = rt.ATTACHED_COMMENT_SIZE,
        .ac_fl = rt.ATTACHED_COMMENT_FLAGS_OFFSET,
        .ac_vs = rt.ATTACHED_COMMENT_VALUE_START_OFFSET,
        .ac_ve = rt.ATTACHED_COMMENT_VALUE_END_OFFSET,
        .fe = flags_expr,
        .f0e = f0_expr,
        .d0 = rt.NODE_HEADER_U32S,
        .d1 = rt.NODE_HEADER_U32S + 1,
        .d2 = rt.NODE_HEADER_U32S + 2,
        .d3 = rt.NODE_HEADER_U32S + 3,
        .d4 = rt.NODE_HEADER_U32S + 4,
        .d5 = rt.NODE_HEADER_U32S + 5,
        .d6 = rt.NODE_HEADER_U32S + 6,
        .d7 = rt.NODE_HEADER_U32S + 7,
        .ss = rt.NODE_SPAN_START_U32,
        .se = rt.NODE_SPAN_END_U32,
    });
    try writeNodeCases(w);
    switch (mode) {
        .parser => try w.writeAll(
            \\    }
            \\  }
            \\  const _inner = _attached ? nodeWithComments : _decode;
            \\  function node(i) { return applyDialectOverlay(i, _inner(i)); }
            \\
        ),
        .analyzer => try w.writeAll(
            \\    }
            \\  }
            \\  const _inner = _attached ? nodeWithComments : _decode;
            \\  const _nodes = Array.from({ length: nodeCount });
            \\  const _nodeIndexes = new WeakMap();
            \\  function node(i) {
            \\    const m = _nodes[i];
            \\    if (m !== undefined) return m;
            \\    const r = applyDialectOverlay(i, _inner(i));
            \\    _nodes[i] = r;
            \\    if (r !== null && typeof r === "object" && !_nodeIndexes.has(r)) _nodeIndexes.set(r, i);
            \\    return r;
            \\  }
            \\
        ),
    }
    if (mode == .analyzer) try w.print(
        \\  const _nodesU32 = _nodesOff >> 2;
        \\  function startOf(i) {{ return _p(_u32[_nodesU32 + i * {[stride]d} + {[ss]d}]); }}
        \\  function endOf(i) {{ return _p(_u32[_nodesU32 + i * {[stride]d} + {[se]d}]); }}
        \\
    , .{
        .stride = rt.NODE_SIZE / 4,
        .ss = rt.NODE_SPAN_START_U32,
        .se = rt.NODE_SPAN_END_U32,
    });
}

fn writeLookupTables(w: *Writer) !void {
    try writeArray(w, "BINARY_OPS", &meta.BINARY_OPS);
    try writeArray(w, "LOGICAL_OPS", &meta.LOGICAL_OPS);
    try writeArray(w, "UNARY_OPS", &meta.UNARY_OPS);
    try writeArray(w, "UPDATE_OPS", &meta.UPDATE_OPS);
    try writeArray(w, "ASSIGNMENT_OPS", &meta.ASSIGNMENT_OPS);
    try writeArray(w, "VAR_KINDS", &meta.VAR_KINDS);
    try writeArray(w, "PROPERTY_KINDS", &meta.PROPERTY_KINDS);
    try writeArray(w, "METHOD_KINDS", &meta.METHOD_KINDS);
    try writeArray(w, "FUNCTION_TYPES", &meta.FUNCTION_TYPES);
    try writeArray(w, "CLASS_TYPES", &meta.CLASS_TYPES);
    try writeArray(w, "SEVERITY", &meta.SEVERITY);
    try writeArrayRaw(w, "IMPORT_EXPORT_KINDS", &meta.IMPORT_EXPORT_KINDS_RAW);
    try writeArrayRaw(w, "ACCESSIBILITY", &meta.ACCESSIBILITY_RAW);
    try writeArrayRaw(w, "TS_TYPE_OPERATORS", &meta.TS_TYPE_OPERATORS_RAW);
    try writeArrayRaw(w, "TS_METHOD_SIGNATURE_KINDS", &meta.TS_METHOD_SIGNATURE_KINDS_RAW);
    try writeArrayRaw(w, "TS_MODULE_KINDS", &meta.TS_MODULE_KINDS_RAW);
    try writeArrayRaw(w, "TS_MAPPED_OPTIONAL", &meta.TS_MAPPED_OPTIONAL_RAW);
    try writeArrayRaw(w, "TS_MAPPED_READONLY", &meta.TS_MAPPED_READONLY_RAW);
    try writeArray(w, "AC_POSITIONS", &.{ "before", "after", "inside" });
}

fn writeArray(w: *Writer, name: []const u8, items: []const []const u8) !void {
    try w.print("const {s} = [", .{name});
    for (items, 0..) |item, i| {
        if (i > 0) try w.writeAll(", ");
        try w.print("\"{s}\"", .{item});
    }
    try w.writeAll("];\n");
}

fn writeArrayRaw(w: *Writer, name: []const u8, items: []const []const u8) !void {
    try w.print("const {s} = [", .{name});
    for (items, 0..) |item, i| {
        if (i > 0) try w.writeAll(", ");
        try w.writeAll(item);
    }
    try w.writeAll("];\n");
}

// child keys of a special-shaped variant, empty types means transparent and never a node itself
const SpecialChildKeys = struct {
    variant: []const u8,
    types: []const []const u8,
    keys: []const []const u8,
};

const special_child_keys = [_]SpecialChildKeys{
    .{ .variant = "formal_parameter", .types = &.{}, .keys = &.{} },
    .{ .variant = "formal_parameters", .types = &.{}, .keys = &.{} },
    .{ .variant = "function", .types = &.{
        "FunctionDeclaration", "FunctionExpression",
        "TSDeclareFunction",   "TSEmptyBodyFunctionExpression",
    }, .keys = &.{ "id", "typeParameters", "params", "returnType", "body" } },
    .{ .variant = "arrow_function_expression", .types = &.{"ArrowFunctionExpression"}, .keys = &.{
        "typeParameters", "params", "returnType", "body",
    } },
    .{ .variant = "program", .types = &.{"Program"}, .keys = &.{ "hashbang", "body" } },
    .{ .variant = "directive", .types = &.{"ExpressionStatement"}, .keys = &.{"expression"} },
    .{ .variant = "string_literal", .types = &.{"Literal"}, .keys = &.{} },
    .{ .variant = "numeric_literal", .types = &.{"Literal"}, .keys = &.{} },
    .{ .variant = "bigint_literal", .types = &.{"Literal"}, .keys = &.{} },
    .{ .variant = "boolean_literal", .types = &.{"Literal"}, .keys = &.{} },
    .{ .variant = "null_literal", .types = &.{"Literal"}, .keys = &.{} },
    .{ .variant = "regexp_literal", .types = &.{"Literal"}, .keys = &.{} },
    .{ .variant = "template_element", .types = &.{"TemplateElement"}, .keys = &.{} },
    .{ .variant = "class", .types = &.{ "ClassDeclaration", "ClassExpression" }, .keys = &.{
        "decorators", "id",                 "typeParameters",
        "superClass", "superTypeArguments", "implements",
        "body",
    } },
    .{ .variant = "method_definition", .types = &.{
        "MethodDefinition", "TSAbstractMethodDefinition",
    }, .keys = &.{ "decorators", "key", "value" } },
    .{ .variant = "property_definition", .types = &.{
        "PropertyDefinition",           "AccessorProperty",
        "TSAbstractPropertyDefinition", "TSAbstractAccessorProperty",
    }, .keys = &.{ "decorators", "key", "typeAnnotation", "value" } },
    .{ .variant = "unary_expression", .types = &.{"UnaryExpression"}, .keys = &.{"argument"} },
    .{ .variant = "binding_property", .types = &.{"Property"}, .keys = &.{ "key", "value" } },
    .{ .variant = "array_pattern", .types = &.{"ArrayPattern"}, .keys = &.{
        "decorators", "elements", "typeAnnotation",
    } },
    .{ .variant = "object_pattern", .types = &.{"ObjectPattern"}, .keys = &.{
        "decorators", "properties", "typeAnnotation",
    } },
    .{ .variant = "jsx_text", .types = &.{"JSXText"}, .keys = &.{} },
    .{ .variant = "ts_function_type", .types = &.{"TSFunctionType"}, .keys = &.{
        "typeParameters", "params", "returnType",
    } },
    .{ .variant = "ts_constructor_type", .types = &.{"TSConstructorType"}, .keys = &.{
        "typeParameters", "params", "returnType",
    } },
    .{ .variant = "ts_method_signature", .types = &.{"TSMethodSignature"}, .keys = &.{
        "key", "typeParameters", "params", "returnType",
    } },
    .{ .variant = "ts_call_signature_declaration", .types = &.{"TSCallSignatureDeclaration"}, .keys = &.{
        "typeParameters", "params", "returnType",
    } },
    .{ .variant = "ts_construct_signature_declaration", .types = &.{"TSConstructSignatureDeclaration"}, .keys = &.{
        "typeParameters", "params", "returnType",
    } },
    .{ .variant = "ts_mapped_type", .types = &.{"TSMappedType"}, .keys = &.{
        "key", "constraint", "nameType", "typeAnnotation",
    } },
    .{ .variant = "ts_module_declaration", .types = &.{"TSModuleDeclaration"}, .keys = &.{ "id", "body" } },
    .{ .variant = "ts_global_declaration", .types = &.{"TSModuleDeclaration"}, .keys = &.{ "id", "body" } },
    .{ .variant = "ts_this_parameter", .types = &.{"Identifier"}, .keys = &.{"typeAnnotation"} },
};

fn specialChildKeysOf(comptime name: []const u8) ?SpecialChildKeys {
    inline for (special_child_keys) |entry| {
        if (comptime std.mem.eql(u8, entry.variant, name)) return entry;
    }
    return null;
}

/// Generates the yuku-ast traversal tables as TypeScript: `CHILD_KEYS`
/// drives walk order and `NODE_TYPES` lists every ESTree `type` string,
/// both derived from the same AST definition as the decoders.
pub fn generateWalkTables(w: *Writer) !void {
    @setEvalBranchQuota(1_000_000);
    try w.writeAll(
        \\// generated by tools/estree/decoder.zig, do not edit
        \\const KEYS: Record<string, string[]> = Object.create(null);
        \\function ck(type: string, keys: string[]): void {
        \\  const prev = KEYS[type];
        \\  if (prev === undefined) KEYS[type] = keys;
        \\  else for (const k of keys) if (!prev.includes(k)) prev.push(k);
        \\}
        \\
    );
    inline for (@typeInfo(ast.NodeData).@"union".fields) |field| {
        if (comptime !meta.includeNode(field.name)) continue;
        if (comptime specialChildKeysOf(field.name)) |entry| {
            inline for (entry.types) |t| {
                try w.print("ck(\"{s}\", [", .{t});
                inline for (entry.keys, 0..) |k, i| {
                    if (i > 0) try w.writeAll(", ");
                    try w.print("\"{s}\"", .{k});
                }
                try w.writeAll("]);\n");
            }
        } else {
            try w.print("ck(\"{s}\", [", .{comptime meta.estreeType(field.name)});
            if (@typeInfo(field.type) == .@"struct") {
                comptime var first = true;
                inline for (std.meta.fields(field.type)) |f| {
                    if (f.type == ast.NodeIndex or f.type == ast.IndexRange) {
                        if (!first) try w.writeAll(", ");
                        try w.print("\"{s}\"", .{comptime meta.estreeField(field.name, f.name)});
                        first = false;
                    }
                }
            }
            try w.writeAll("]);\n");
        }
    }
    try w.writeAll("ck(\"Hashbang\", []);\n");
    try w.writeAll(
        \\
        \\export const CHILD_KEYS: Readonly<Record<string, readonly string[]>> = KEYS;
        \\
        \\const TYPES = [
        \\
    );
    inline for (@typeInfo(ast.NodeData).@"union".fields) |field| {
        if (comptime !meta.includeNode(field.name)) continue;
        if (comptime specialChildKeysOf(field.name)) |entry| {
            inline for (entry.types) |t| {
                try w.print("  \"{s}\",\n", .{t});
            }
        } else {
            try w.print("  \"{s}\",\n", .{comptime meta.estreeType(field.name)});
        }
    }
    try w.writeAll(
        \\  "Hashbang",
        \\] as const;
        \\
        \\export type GeneratedNodeType = (typeof TYPES)[number];
        \\
        \\export const NODE_TYPES: readonly GeneratedNodeType[] = [...new Set<GeneratedNodeType>(TYPES)];
        \\
    );
}

fn writeChildTables(w: *Writer) !void {
    @setEvalBranchQuota(1_000_000);
    // kinds: 0 NodeIndex, 1 range with length in slot+1,
    //        2 range with length in field0, 3 range with length in field0b
    try w.writeAll("const CHILD_SLOTS = [\n");
    inline for (@typeInfo(ast.NodeData).@"union".fields) |field| {
        if (comptime !meta.includeNode(field.name)) continue;
        try w.writeAll("  [");
        if (@typeInfo(field.type) == .@"struct") {
            comptime var first = true;
            inline for (std.meta.fields(field.type), 0..) |f, i| {
                if (f.type == ast.NodeIndex or f.type == ast.IndexRange) {
                    if (!first) try w.writeAll(", ");
                    const kind: u32 = if (f.type == ast.NodeIndex)
                        0
                    else switch (comptime rt.rangeIndexOf(field.type, i)) {
                        0 => 2,
                        1 => 3,
                        else => 1,
                    };
                    try w.print("{d}, {d}", .{
                        kind,
                        comptime rt.u32SlotForField(field.type, i) + rt.NODE_HEADER_U32S,
                    });
                    first = false;
                }
            }
        }
        try w.writeAll("],\n");
    }
    try w.writeAll("];\n");

    try w.writeAll("const IS_NODE = [\n");
    inline for (@typeInfo(ast.NodeData).@"union".fields) |field| {
        if (comptime !meta.includeNode(field.name)) continue;
        const materialized = comptime if (specialChildKeysOf(field.name)) |entry|
            entry.types.len != 0
        else
            true;
        try w.print("  {},\n", .{materialized});
    }
    try w.writeAll("];\n");
}

/// number of IndexRange fields in struct T.
fn rangeCount(comptime T: type) usize {
    comptime {
        if (@typeInfo(T) != .@"struct") return 0;
        var n: usize = 0;
        for (std.meta.fields(T)) |f| {
            if (f.type == ast.IndexRange) n += 1;
        }
        return n;
    }
}

fn isIdentChar(c: u8) bool {
    return (c >= 'a' and c <= 'z') or (c >= 'A' and c <= 'Z') or
        (c >= '0' and c <= '9') or c == '_' or c == '$';
}

/// whether `body` references `name` as a standalone identifier, so that
/// e.g. `f0` does not match inside `f0b`.
fn usesIdent(body: []const u8, name: []const u8) bool {
    var i: usize = 0;
    while (std.mem.indexOfPos(u8, body, i, name)) |p| : (i = p + 1) {
        const before_ok = p == 0 or !isIdentChar(body[p - 1]);
        const after = p + name.len;
        const after_ok = after >= body.len or !isIdentChar(body[after]);
        if (before_ok and after_ok) return true;
    }
    return false;
}

/// opens `case N: {` and declares only the u32 slots the rendered case
/// body references: `f0`/`f0b` (the packed u16 lengths of the first and
/// second IndexRange) and `f1..fn` for the type's data slots. cases
/// therefore only load the words they read, and special-cased bodies
/// that skip fields never see dead declarations.
fn writeCaseOpen(w: *Writer, comptime tag: usize, comptime T: type, body: []const u8) !void {
    try w.print("    case {d}: {{", .{tag});
    if (@typeInfo(T) == .@"struct") {
        const ranges = comptime rangeCount(T);
        if (ranges >= 1 and usesIdent(body, "f0")) {
            try w.print(" const f0 = {s};", .{comptime u16At(
                std.fmt.comptimePrint("_u32[b + {d}]", .{rt.NODE_FIELD0_OFFSET / 4}),
                rt.NODE_FIELD0_OFFSET % 4,
            )});
        }
        if (ranges >= 2 and usesIdent(body, "f0b")) {
            try w.print(" const f0b = {s};", .{comptime u16At(
                std.fmt.comptimePrint("_u32[b + {d}]", .{rt.NODE_FIELD0B_OFFSET / 4}),
                rt.NODE_FIELD0B_OFFSET % 4,
            )});
        }
        const n = comptime rt.totalU32Slots(T);
        var first = true;
        inline for (0..n) |k| {
            if (usesIdent(body, std.fmt.comptimePrint("f{d}", .{k + 1}))) {
                try w.writeAll(if (first) " const " else ", ");
                first = false;
                try w.print("f{d} = _u32[b + {d}]", .{ k + 1, k + rt.NODE_HEADER_U32S });
            }
        }
        if (!first) try w.writeAll(";");
    }
    if (body.len > 0 and body[0] != '\n') try w.writeAll(" ");
}

fn writeNodeCases(w: *Writer) !void {
    @setEvalBranchQuota(100_000);
    var body_buf: [16 * 1024]u8 = undefined;
    inline for (@typeInfo(ast.NodeData).@"union".fields, 0..) |field, tag| {
        if (comptime !meta.includeNode(field.name)) continue;
        var body_w: Writer = .fixed(&body_buf);
        if (comptime isSpecial(field.name)) {
            try writeSpecialCase(&body_w, field.name);
        } else {
            try writeGenericCase(&body_w, field.name, field.type);
        }
        const body = body_w.buffered();
        try writeCaseOpen(w, tag, field.type, body);
        try w.writeAll(body);
        try w.writeAll(" }\n");
    }
    if (comptime dialect_enabled) {
        try w.print("    case {d}: {{ const f1 = _u32[b + {d}]; return dialectRecord(f1, start, end, false); }}\n", .{
            rt.dialectNodeTag(),
            rt.NODE_HEADER_U32S,
        });
    }
}

fn writeGenericCase(
    w: *Writer,
    comptime name: []const u8,
    comptime T: type,
) !void {
    if (comptime std.mem.eql(u8, name, "dialect_node")) {
        try w.writeAll("return dialectRecord(f1, start, end, false);");
        return;
    }
    const etype = comptime meta.estreeType(name);
    const has_ts = comptime hasAnyTsField(name, T) or tsExtrasOf(name).len > 0;
    if (!has_ts) {
        try w.print("return {{ type: \"{s}\", start, end", .{etype});
        try writeStructFields(w, name, T, .all);
        try w.writeAll(" };");
        return;
    }
    try w.print("const r = {{ type: \"{s}\", start, end", .{etype});
    try writeStructFields(w, name, T, .non_ts);
    try w.writeAll(" }; if (_isTs) { ");
    try writeStructFields(w, name, T, .ts_only);
    try writeTsExtras(w, name);
    try w.writeAll("} return r;");
}

const FieldSelection = enum { all, non_ts, ts_only };

fn writeStructFields(
    w: *Writer,
    comptime tag_name: []const u8,
    comptime T: type,
    comptime sel: FieldSelection,
) !void {
    if (@typeInfo(T) != .@"struct") return;
    inline for (std.meta.fields(T), 0..) |f, i| {
        const is_ts = comptime isTsField(tag_name, f.name);
        const include = switch (sel) {
            .all => true,
            .non_ts => !is_ts,
            .ts_only => is_ts,
        };
        if (!include) continue;
        const js = comptime meta.estreeField(tag_name, f.name);
        if (sel == .ts_only) try w.print("r.{s} = ", .{js}) else try w.print(", {s}: ", .{js});
        try writeFieldExpr(w, tag_name, f.name, T, i, f.type);
        if (sel == .ts_only) try w.writeAll("; ");
    }
}

fn writeFieldExpr(
    w: *Writer,
    comptime tag_name: []const u8,
    comptime field_name: []const u8,
    comptime T: type,
    comptime i: usize,
    comptime F: type,
) !void {
    const s = comptime rt.u32SlotForField(T, i) + 1;
    if (F == u32) {
        try w.print("f{d}", .{s});
    } else if (F == ast.NodeIndex) {
        try w.print("f{d} !== NULL ? node(f{d}) : null", .{ s, s });
    } else if (F == ast.IndexRange) {
        const fn_name = comptime if (meta.isHoleyArray(tag_name, field_name)) "nodeArrHoles" else "nodeArr";
        switch (comptime rt.rangeIndexOf(T, i)) {
            0 => try w.print("{s}(f{d}, f0)", .{ fn_name, s }),
            1 => try w.print("{s}(f{d}, f0b)", .{ fn_name, s }),
            else => try w.print("{s}(f{d}, f{d})", .{ fn_name, s, s + 1 }),
        }
    } else if (F == ast.String) {
        try w.print("str(f{d}, f{d})", .{ s, s + 1 });
    } else if (F == bool) {
        try w.print("!!(flags & {d})", .{comptime flagMaskAt(T, i)});
    } else if (comptime rt.isEnumType(F)) {
        const bit = comptime rt.flagBitForField(T, i);
        const mask = comptime enumMask(F);
        const table = comptime meta.enumTableName(F);
        if (bit == 0) {
            try w.print("{s}[flags & {d}]", .{ table, mask });
        } else {
            try w.print("{s}[(flags >> {d}) & {d}]", .{ table, bit, mask });
        }
    } else if (F == ?ast.ImportPhase) {
        const bit = comptime rt.flagBitForField(T, i);
        try w.print(
            "(flags & {d}) ? [\"source\", \"defer\"][(flags >> {d}) & 1] : null",
            .{ @as(u32, 1) << @intCast(bit), bit + 1 },
        );
    } else if (F == ?ast.Hashbang) {
        const bit = comptime rt.flagBitForField(T, i);
        try w.print(
            "(flags & {d}) ? str(f{d}, f{d}) : null",
            .{ @as(u32, 1) << @intCast(bit), s, s + 1 },
        );
    } else {
        @compileError("unsupported field type in decoder: " ++ @typeName(F));
    }
}

fn isSpecial(comptime name: []const u8) bool {
    return specialChildKeysOf(name) != null;
}

fn writeSpecialCase(w: *Writer, comptime name: []const u8) !void {
    const eql = std.mem.eql;
    if (comptime eql(u8, name, "formal_parameter")) {
        const sp = comptime slotOf(ast.FormalParameter, "pattern");
        try emit(w, "return node(f{d});", .{sp});
    } else if (comptime eql(u8, name, "formal_parameters")) {
        try emit(w, "return {{ params: fnParams(i) }};", .{});
    } else if (comptime eql(u8, name, "function")) {
        const sid = comptime slotOf(ast.Function, "id");
        const sp = comptime slotOf(ast.Function, "params");
        const sb = comptime slotOf(ast.Function, "body");
        const stp = comptime slotOf(ast.Function, "type_parameters");
        const srt = comptime slotOf(ast.Function, "return_type");
        const bg = comptime flagMask(ast.Function, "generator");
        const ba = comptime flagMask(ast.Function, "async");
        const bd = comptime flagMask(ast.Function, "declare");
        try emit(w,
            \\
            \\      const ft = flags & {d};
            \\      const r = {{
            \\        type: FUNCTION_TYPES[ft], start, end,
            \\        id: f{d} !== NULL ? node(f{d}) : null,
            \\        generator: !!(flags & {d}), async: !!(flags & {d}),
            \\        params: f{d} !== NULL ? fnParams(f{d}) : [],
            \\        body: f{d} !== NULL ? node(f{d}) : null,
            \\        expression: false,
            \\      }};
            \\      if (_isTs) {{
            \\        r.typeParameters = f{d} !== NULL ? node(f{d}) : null;
            \\        r.returnType = f{d} !== NULL ? node(f{d}) : null;
            \\        r.declare = !!(flags & {d});
            \\      }}
            \\      return r;
        , .{
            comptime enumMask(ast.FunctionType),
            sid,
            sid,
            bg,
            ba,
            sp,
            sp,
            sb,
            sb,
            stp,
            stp,
            srt,
            srt,
            bd,
        });
    } else if (comptime eql(u8, name, "arrow_function_expression")) {
        const sp = comptime slotOf(ast.ArrowFunctionExpression, "params");
        const sb = comptime slotOf(ast.ArrowFunctionExpression, "body");
        const stp = comptime slotOf(ast.ArrowFunctionExpression, "type_parameters");
        const srt = comptime slotOf(ast.ArrowFunctionExpression, "return_type");
        const be = comptime flagMask(ast.ArrowFunctionExpression, "expression");
        const ba = comptime flagMask(ast.ArrowFunctionExpression, "async");
        try emit(w,
            \\
            \\      const r = {{
            \\        type: "ArrowFunctionExpression", start, end,
            \\        id: null, generator: false, async: !!(flags & {d}),
            \\        params: f{d} !== NULL ? fnParams(f{d}) : [],
            \\        body: node(f{d}), expression: !!(flags & {d}),
            \\      }};
            \\      if (_isTs) {{
            \\        r.typeParameters = f{d} !== NULL ? node(f{d}) : null;
            \\        r.returnType = f{d} !== NULL ? node(f{d}) : null;
            \\      }}
            \\      return r;
        , .{ ba, sp, sp, sb, be, stp, stp, srt, srt });
    } else if (comptime eql(u8, name, "program")) {
        const sb = comptime slotOf(ast.Program, "body");
        const hs = comptime slotOf(ast.Program, "hashbang");
        // hashbang span includes the leading #!, so its start is value.start minus 2
        if (comptime dialect_enabled) try emit(w,
            \\const r = {{
            \\      type: "Program", start, end,
            \\      sourceType: (flags & 1) ? "module" : "script",
            \\      hashbang: (flags & {d}) ? {{
            \\        type: "Hashbang",
            \\        start: _p(f{d} - 2), end: _p(f{d}),
            \\        value: str(f{d}, f{d}),
            \\      }} : null,
            \\      body: nodeArr(f{d}, f0),
            \\    }};
            \\      if (_isTs) Object.defineProperty(r, DIALECT_TS, {{
            \\        value: true, enumerable: false, writable: false, configurable: false,
            \\      }});
            \\      return r;
        , .{ comptime flagMask(ast.Program, "hashbang"), hs, hs + 1, hs, hs + 1, sb }) else try emit(w,
            \\return {{
            \\      type: "Program", start, end,
            \\      sourceType: (flags & 1) ? "module" : "script",
            \\      hashbang: (flags & {d}) ? {{
            \\        type: "Hashbang",
            \\        start: _p(f{d} - 2), end: _p(f{d}),
            \\        value: str(f{d}, f{d}),
            \\      }} : null,
            \\      body: nodeArr(f{d}, f0),
            \\    }};
        , .{ comptime flagMask(ast.Program, "hashbang"), hs, hs + 1, hs, hs + 1, sb });
    } else if (comptime eql(u8, name, "directive")) {
        const se = comptime slotOf(ast.Directive, "expression");
        const sv = comptime slotOf(ast.Directive, "value");
        try emit(w,
            \\return {{
            \\      type: "ExpressionStatement", start, end,
            \\      expression: node(f{d}), directive: str(f{d}, f{d}),
            \\    }};
        , .{ se, sv, sv + 1 });
    } else if (comptime eql(u8, name, "string_literal")) {
        const sv = comptime slotOf(ast.StringLiteral, "value");
        try emit(w,
            \\return {{
            \\      type: "Literal", start, end,
            \\      value: str(f{d}, f{d}), raw: _src.slice(start, end),
            \\    }};
        , .{ sv, sv + 1 });
    } else if (comptime eql(u8, name, "numeric_literal")) {
        try emit(w,
            \\
            \\      const r = _src.slice(start, end);
            \\      const s = r.indexOf("_") === -1 ? r : r.replace(/_/g, "");
            \\      const v = (flags & {d}) === 2 && s[1] !== "o" && s[1] !== "O"
            \\        ? parseInt(s.slice(1), 8)
            \\        : +s;
            \\      return {{
            \\        type: "Literal", start, end,
            \\        value: v,
            \\        raw: r,
            \\      }};
        , .{comptime enumMask(ast.NumericLiteral.Kind)});
    } else if (comptime eql(u8, name, "bigint_literal")) {
        const sr = comptime slotOf(ast.BigIntLiteral, "raw");
        try emit(w,
            \\
            \\      const r = _src.slice(start, end);
            \\      const d = str(f{d}, f{d}).replace(/_/g, "");
            \\      const v = BigInt(d);
            \\      return {{
            \\        type: "Literal", start, end,
            \\        value: v, raw: r, bigint: v.toString(),
            \\      }};
        , .{ sr, sr + 1 });
    } else if (comptime eql(u8, name, "boolean_literal")) {
        try emit(w,
            \\
            \\      const v = !!(flags & {d});
            \\      return {{
            \\        type: "Literal", start, end,
            \\        value: v, raw: v ? "true" : "false",
            \\      }};
        , .{comptime flagMaskAt(ast.BooleanLiteral, 0)});
    } else if (comptime eql(u8, name, "null_literal")) {
        try emit(w,
            \\return {{ type: "Literal", start, end, value: null, raw: "null" }};
        , .{});
    } else if (comptime eql(u8, name, "regexp_literal")) {
        const sp = comptime slotOf(ast.RegExpLiteral, "pattern");
        const sf = comptime slotOf(ast.RegExpLiteral, "flags");
        try emit(w,
            \\
            \\      const p = str(f{d}, f{d}), fl = str(f{d}, f{d});
            \\      let v = null;
            \\      try {{ v = new RegExp(p, fl); }} catch {{}}
            \\      return {{
            \\        type: "Literal", start, end,
            \\        value: v, raw: "/" + p + "/" + fl,
            \\        regex: {{ pattern: p, flags: fl.split("").sort().join("") }},
            \\      }};
        , .{ sp, sp + 1, sf, sf + 1 });
    } else if (comptime eql(u8, name, "template_element")) {
        const sc = comptime slotOf(ast.TemplateElement, "cooked");
        try emit(w,
            \\
            \\      const raw = _src.slice(start, end).replace(/\r\n?/g, "\n");
            \\      const tl = !!(flags & {d});
            \\      const s = _isTs ? start - 1 : start;
            \\      const e = _isTs ? (tl ? end + 1 : end + 2) : end;
            \\      return {{
            \\        type: "TemplateElement", start: s, end: e,
            \\        value: {{
            \\          raw,
            \\          cooked: (flags & {d}) ? null : str(f{d}, f{d}),
            \\        }},
            \\        tail: tl,
            \\      }};
        , .{
            comptime flagMask(ast.TemplateElement, "tail"),
            comptime flagMask(ast.TemplateElement, "is_cooked_undefined"),
            sc,
            sc + 1,
        });
    } else if (comptime eql(u8, name, "class")) {
        const sd = comptime slotOf(ast.Class, "decorators");
        const si = comptime slotOf(ast.Class, "id");
        const ss = comptime slotOf(ast.Class, "super_class");
        const sb = comptime slotOf(ast.Class, "body");
        const stp = comptime slotOf(ast.Class, "type_parameters");
        const ssta = comptime slotOf(ast.Class, "super_type_arguments");
        const simp = comptime slotOf(ast.Class, "implements");
        try emit(w,
            \\
            \\      const r = {{
            \\        type: CLASS_TYPES[flags & {d}], start, end,
            \\        decorators: nodeArr(f{d}, f0),
            \\        id: f{d} !== NULL ? node(f{d}) : null,
            \\        superClass: f{d} !== NULL ? node(f{d}) : null,
            \\        body: node(f{d}),
            \\      }};
            \\      if (_isTs) {{
            \\        r.typeParameters = f{d} !== NULL ? node(f{d}) : null;
            \\        r.superTypeArguments = f{d} !== NULL ? node(f{d}) : null;
            \\        r.implements = nodeArr(f{d}, f0b);
            \\        r.abstract = !!(flags & {d});
            \\        r.declare = !!(flags & {d});
            \\      }}
            \\      return r;
        , .{
            comptime enumMask(ast.ClassType),
            sd,
            si,
            si,
            ss,
            ss,
            sb,
            stp,
            stp,
            ssta,
            ssta,
            simp,
            comptime flagMask(ast.Class, "abstract"),
            comptime flagMask(ast.Class, "declare"),
        });
    } else if (comptime eql(u8, name, "method_definition")) {
        const M = ast.MethodDefinition;
        const sd = comptime slotOf(M, "decorators");
        const sk = comptime slotOf(M, "key");
        const sv = comptime slotOf(M, "value");
        try emit(w,
            \\
            \\      const r = {{
            \\        type: "MethodDefinition", start, end,
            \\        decorators: nodeArr(f{d}, f0),
            \\        key: node(f{d}), value: node(f{d}),
            \\        kind: METHOD_KINDS[flags & {d}],
            \\        computed: !!(flags & {d}), static: !!(flags & {d}),
            \\      }};
            \\      if (_isTs) {{
            \\        r.override = !!(flags & {d});
            \\        r.optional = !!(flags & {d});
            \\        const _abs = !!(flags & {d});
            \\        r.accessibility = ACCESSIBILITY[(flags >> {d}) & {d}];
            \\        if (_abs) r.type = "TSAbstractMethodDefinition";
            \\      }}
            \\      return r;
        , .{
            sd,
            sk,
            sv,
            comptime enumMask(ast.MethodDefinitionKind),
            comptime flagMask(M, "computed"),
            comptime flagMask(M, "static"),
            comptime flagMask(M, "override"),
            comptime flagMask(M, "optional"),
            comptime flagMask(M, "abstract"),
            comptime flagBit(M, "accessibility"),
            comptime enumMask(ast.Accessibility),
        });
    } else if (comptime eql(u8, name, "property_definition")) {
        const P = ast.PropertyDefinition;
        const sd = comptime slotOf(P, "decorators");
        const sk = comptime slotOf(P, "key");
        const sv = comptime slotOf(P, "value");
        const sta = comptime slotOf(P, "type_annotation");
        try emit(w,
            \\
            \\      const _acc = !!(flags & {d});
            \\      const r = {{
            \\        type: _acc ? "AccessorProperty" : "PropertyDefinition",
            \\        start, end,
            \\        decorators: nodeArr(f{d}, f0),
            \\        key: node(f{d}),
            \\        value: f{d} !== NULL ? node(f{d}) : null,
            \\        computed: !!(flags & {d}), static: !!(flags & {d}),
            \\      }};
            \\      if (_isTs) {{
            \\        r.typeAnnotation = f{d} !== NULL ? node(f{d}) : null;
            \\        r.declare = !!(flags & {d});
            \\        r.override = !!(flags & {d});
            \\        r.optional = !!(flags & {d});
            \\        r.definite = !!(flags & {d});
            \\        r.readonly = !!(flags & {d});
            \\        const _abs = !!(flags & {d});
            \\        r.accessibility = ACCESSIBILITY[(flags >> {d}) & {d}];
            \\        if (_abs)
            \\          r.type = _acc
            \\            ? "TSAbstractAccessorProperty"
            \\            : "TSAbstractPropertyDefinition";
            \\      }}
            \\      return r;
        , .{
            comptime flagMask(P, "accessor"),
            sd,
            sk,
            sv,
            sv,
            comptime flagMask(P, "computed"),
            comptime flagMask(P, "static"),
            sta,
            sta,
            comptime flagMask(P, "declare"),
            comptime flagMask(P, "override"),
            comptime flagMask(P, "optional"),
            comptime flagMask(P, "definite"),
            comptime flagMask(P, "readonly"),
            comptime flagMask(P, "abstract"),
            comptime flagBit(P, "accessibility"),
            comptime enumMask(ast.Accessibility),
        });
    } else if (comptime eql(u8, name, "unary_expression")) {
        const sa = comptime slotOf(ast.UnaryExpression, "argument");
        try emit(w,
            \\return {{
            \\      type: "UnaryExpression", start, end,
            \\      operator: UNARY_OPS[flags & {d}], prefix: true,
            \\      argument: f{d} !== NULL ? node(f{d}) : null,
            \\    }};
        , .{ comptime enumMask(ast.UnaryOperator), sa, sa });
    } else if (comptime eql(u8, name, "binding_property")) {
        const sk = comptime slotOf(ast.BindingProperty, "key");
        const sv = comptime slotOf(ast.BindingProperty, "value");
        try w.print(
            \\
            \\      const r = {{
            \\        type: "Property", start, end,
            \\        kind: "init",
            \\        key: node(f{d}), value: node(f{d}),
            \\        method: false,
            \\        shorthand: !!(flags & {d}),
            \\        computed: !!(flags & {d}),
            \\      }};
        , .{
            sk,
            sv,
            comptime flagMask(ast.BindingProperty, "shorthand"),
            comptime flagMask(ast.BindingProperty, "computed"),
        });
        try w.writeAll(" if (_isTs) { ");
        try writeTsExtras(w, "binding_property");
        try w.writeAll("} return r;");
    } else if (comptime eql(u8, name, "array_pattern")) {
        const se = comptime slotOf(ast.ArrayPattern, "elements");
        const sr = comptime slotOf(ast.ArrayPattern, "rest");
        const sdec = comptime slotOf(ast.ArrayPattern, "decorators");
        const sta = comptime slotOf(ast.ArrayPattern, "type_annotation");
        try emit(w,
            \\
            \\      const el = nodeArrHoles(f{d}, f0b);
            \\      if (f{d} !== NULL) el.push(node(f{d}));
            \\      const r = {{ type: "ArrayPattern", start, end, elements: el }};
            \\      if (_isTs) {{
            \\        r.decorators = nodeArr(f{d}, f0);
            \\        r.optional = !!(flags & {d});
            \\        r.typeAnnotation = f{d} !== NULL ? node(f{d}) : null;
            \\      }}
            \\      return r;
        , .{
            se,                                              sr,
            sr,                                              sdec,
            comptime flagMask(ast.ArrayPattern, "optional"), sta,
            sta,
        });
    } else if (comptime eql(u8, name, "object_pattern")) {
        const sp = comptime slotOf(ast.ObjectPattern, "properties");
        const sr = comptime slotOf(ast.ObjectPattern, "rest");
        const sdec = comptime slotOf(ast.ObjectPattern, "decorators");
        const sta = comptime slotOf(ast.ObjectPattern, "type_annotation");
        try emit(w,
            \\
            \\      const pr = nodeArr(f{d}, f0b);
            \\      if (f{d} !== NULL) pr.push(node(f{d}));
            \\      const r = {{ type: "ObjectPattern", start, end, properties: pr }};
            \\      if (_isTs) {{
            \\        r.decorators = nodeArr(f{d}, f0);
            \\        r.optional = !!(flags & {d});
            \\        r.typeAnnotation = f{d} !== NULL ? node(f{d}) : null;
            \\      }}
            \\      return r;
        , .{
            sp,                                               sr,
            sr,                                               sdec,
            comptime flagMask(ast.ObjectPattern, "optional"), sta,
            sta,
        });
    } else if (comptime eql(u8, name, "jsx_text")) {
        const sv = comptime slotOf(ast.JSXText, "value");
        try emit(w,
            \\
            \\      const t = str(f{d}, f{d});
            \\      return {{ type: "JSXText", start, end, value: t, raw: t }};
        , .{ sv, sv + 1 });
    } else if (comptime eql(u8, name, "ts_function_type")) {
        const stp = comptime slotOf(ast.TSFunctionType, "type_parameters");
        const sp = comptime slotOf(ast.TSFunctionType, "params");
        const srt = comptime slotOf(ast.TSFunctionType, "return_type");
        try emit(w,
            \\return {{
            \\      type: "TSFunctionType", start, end,
            \\      typeParameters: f{d} !== NULL ? node(f{d}) : null,
            \\      params: f{d} !== NULL ? fnParams(f{d}) : [],
            \\      returnType: f{d} !== NULL ? node(f{d}) : null,
            \\    }};
        , .{ stp, stp, sp, sp, srt, srt });
    } else if (comptime eql(u8, name, "ts_constructor_type")) {
        const stp = comptime slotOf(ast.TSConstructorType, "type_parameters");
        const sp = comptime slotOf(ast.TSConstructorType, "params");
        const srt = comptime slotOf(ast.TSConstructorType, "return_type");
        const ba = comptime flagMask(ast.TSConstructorType, "abstract");
        try emit(w,
            \\return {{
            \\      type: "TSConstructorType", start, end,
            \\      abstract: !!(flags & {d}),
            \\      typeParameters: f{d} !== NULL ? node(f{d}) : null,
            \\      params: f{d} !== NULL ? fnParams(f{d}) : [],
            \\      returnType: f{d} !== NULL ? node(f{d}) : null,
            \\    }};
        , .{ ba, stp, stp, sp, sp, srt, srt });
    } else if (comptime eql(u8, name, "ts_method_signature")) {
        const sk = comptime slotOf(ast.TSMethodSignature, "key");
        const stp = comptime slotOf(ast.TSMethodSignature, "type_parameters");
        const sp = comptime slotOf(ast.TSMethodSignature, "params");
        const srt = comptime slotOf(ast.TSMethodSignature, "return_type");
        const kbit = comptime rt.flagBitForField(
            ast.TSMethodSignature,
            fieldIdx(ast.TSMethodSignature, "kind"),
        );
        const kmask = comptime enumMask(ast.TSMethodSignatureKind);
        const bc = comptime flagMask(ast.TSMethodSignature, "computed");
        const bo = comptime flagMask(ast.TSMethodSignature, "optional");
        try emit(w,
            \\return {{
            \\      type: "TSMethodSignature", start, end,
            \\      key: node(f{d}),
            \\      computed: !!(flags & {d}),
            \\      optional: !!(flags & {d}),
            \\      kind: TS_METHOD_SIGNATURE_KINDS[(flags >> {d}) & {d}],
            \\      typeParameters: f{d} !== NULL ? node(f{d}) : null,
            \\      params: f{d} !== NULL ? fnParams(f{d}) : [],
            \\      returnType: f{d} !== NULL ? node(f{d}) : null,
            \\      accessibility: null, readonly: false, static: false,
            \\    }};
        , .{ sk, bc, bo, kbit, kmask, stp, stp, sp, sp, srt, srt });
    } else if (comptime eql(u8, name, "ts_call_signature_declaration")) {
        const stp = comptime slotOf(ast.TSCallSignatureDeclaration, "type_parameters");
        const sp = comptime slotOf(ast.TSCallSignatureDeclaration, "params");
        const srt = comptime slotOf(ast.TSCallSignatureDeclaration, "return_type");
        try emit(w,
            \\return {{
            \\      type: "TSCallSignatureDeclaration", start, end,
            \\      typeParameters: f{d} !== NULL ? node(f{d}) : null,
            \\      params: f{d} !== NULL ? fnParams(f{d}) : [],
            \\      returnType: f{d} !== NULL ? node(f{d}) : null,
            \\    }};
        , .{ stp, stp, sp, sp, srt, srt });
    } else if (comptime eql(u8, name, "ts_mapped_type")) {
        const sk = comptime slotOf(ast.TSMappedType, "key");
        const sc = comptime slotOf(ast.TSMappedType, "constraint");
        const snt = comptime slotOf(ast.TSMappedType, "name_type");
        const sta = comptime slotOf(ast.TSMappedType, "type_annotation");
        const bo = comptime flagBit(ast.TSMappedType, "optional");
        const br = comptime flagBit(ast.TSMappedType, "readonly");
        const mo = comptime enumMask(ast.TSMappedTypeModifier);
        try emit(w,
            \\return {{
            \\      type: "TSMappedType", start, end,
            \\      key: node(f{d}),
            \\      constraint: node(f{d}),
            \\      nameType: f{d} !== NULL ? node(f{d}) : null,
            \\      typeAnnotation: f{d} !== NULL ? node(f{d}) : null,
            \\      optional: TS_MAPPED_OPTIONAL[(flags >> {d}) & {d}],
            \\      readonly: TS_MAPPED_READONLY[(flags >> {d}) & {d}],
            \\    }};
        , .{ sk, sc, snt, snt, sta, sta, bo, mo, br, mo });
    } else if (comptime eql(u8, name, "ts_construct_signature_declaration")) {
        const stp = comptime slotOf(ast.TSConstructSignatureDeclaration, "type_parameters");
        const sp = comptime slotOf(ast.TSConstructSignatureDeclaration, "params");
        const srt = comptime slotOf(ast.TSConstructSignatureDeclaration, "return_type");
        try emit(w,
            \\return {{
            \\      type: "TSConstructSignatureDeclaration", start, end,
            \\      typeParameters: f{d} !== NULL ? node(f{d}) : null,
            \\      params: f{d} !== NULL ? fnParams(f{d}) : [],
            \\      returnType: f{d} !== NULL ? node(f{d}) : null,
            \\    }};
        , .{ stp, stp, sp, sp, srt, srt });
    } else if (comptime eql(u8, name, "ts_module_declaration")) {
        const sid = comptime slotOf(ast.TSModuleDeclaration, "id");
        const sb = comptime slotOf(ast.TSModuleDeclaration, "body");
        const kbit = comptime flagBit(ast.TSModuleDeclaration, "kind");
        const kmask = comptime enumMask(ast.TSModuleDeclarationKind);
        const db = comptime flagMask(ast.TSModuleDeclaration, "declare");
        try emit(w,
            \\
            \\      const r = {{
            \\        type: "TSModuleDeclaration", start, end,
            \\        id: node(f{d}),
            \\        kind: TS_MODULE_KINDS[(flags >> {d}) & {d}],
            \\        declare: !!(flags & {d}),
            \\        global: false,
            \\      }};
            \\      if (f{d} !== NULL) r.body = node(f{d});
            \\      return r;
        , .{ sid, kbit, kmask, db, sb, sb });
    } else if (comptime eql(u8, name, "ts_global_declaration")) {
        const sid = comptime slotOf(ast.TSGlobalDeclaration, "id");
        const sb = comptime slotOf(ast.TSGlobalDeclaration, "body");
        const db = comptime flagMask(ast.TSGlobalDeclaration, "declare");
        try emit(w,
            \\return {{
            \\      type: "TSModuleDeclaration", start, end,
            \\      id: node(f{d}), body: node(f{d}),
            \\      kind: "global",
            \\      declare: !!(flags & {d}),
            \\      global: true,
            \\    }};
        , .{ sid, sb, db });
    } else if (comptime eql(u8, name, "ts_this_parameter")) {
        // an Identifier named this, ts-estree convention, the encoder matches it by that name
        const sta = comptime slotOf(ast.TSThisParameter, "type_annotation");
        try emit(w,
            \\return {{
            \\      type: "Identifier", start, end,
            \\      decorators: [],
            \\      name: "this", optional: false,
            \\      typeAnnotation: f{d} !== NULL ? node(f{d}) : null,
            \\    }};
        , .{ sta, sta });
    }
}

fn writeDecodeBody(w: *Writer, mode: Mode) !void {
    comptime std.debug.assert(rt.COMMENT_FLAGS_OFFSET == 0);
    comptime std.debug.assert(rt.COMMENT_SIZE % 4 == 0);
    if (comptime dialect_enabled) try w.print(
        \\  const _dialectSectionOff = _cOff + commentCount * {[csize]d};
        \\  const _hasDialectRecords = !!(_flags & {[flag]d});
        \\  const _dialectRecordCount = _hasDialectRecords ? _u32[_dialectSectionOff >> 2] : 0;
        \\  const _dialectOverlayCount = _hasDialectRecords ? _u32[(_dialectSectionOff >> 2) + 1] : 0;
        \\  const _dialectRecordsOff = _dialectSectionOff + (_hasDialectRecords ? {[sub]d} : 0);
        \\  const _dialectOverlaysOff = _dialectRecordsOff + _dialectRecordCount * {[node_size]d};
        \\  const dOff = _dialectOverlaysOff + _dialectOverlayCount * {[overlay_size]d};
        \\  const _dialectActive = new Set();
        \\  function dialectRecord(ri, start, end, overlay) {{
        \\    if (ri < 0 || ri >= _dialectRecordCount) throw new RangeError("yuku: dialect record index out of bounds");
        \\    if (_dialectActive.has(ri)) throw new RangeError("yuku: cyclic dialect record");
        \\    _dialectActive.add(ri);
        \\    try {{
        \\      const b = (_dialectRecordsOff >> 2) + ri * ({[node_size]d} >> 2);
        \\      const packedTag = _u32[b] & 255;
        \\      const schema = DIALECT_RECORDS[packedTag - DIALECT_RECORDS[0].tag];
        \\      if (!schema || schema.tag !== packedTag || schema.overlay !== overlay)
        \\        throw new RangeError("yuku: invalid dialect record tag");
        \\      const r = overlay ? {{}} : {{ type: schema.type, start, end }};
        \\      const packedFlags = _u32[b] >>> 16;
        \\      for (const field of schema.fields) {{
        \\        if (field.role === "host") continue;
        \\        const v = _u32[b + field.slot];
        \\        if (field.role === "bool") r[field.name] = !!(packedFlags & (1 << field.bit));
        \\        else if (field.role === "scalar") r[field.name] = v >>> 0;
        \\        else if (field.role === "node") r[field.name] = node(v);
        \\        else if (field.role === "optionalNode") r[field.name] = v === NULL ? null : node(v);
        \\        else if (field.role === "nodeList") r[field.name] = nodeArr(v, _u32[b + field.slot + 1]);
        \\        else if (field.role === "string") r[field.name] = str(v, _u32[b + field.slot + 1]);
        \\      }}
        \\      return r;
        \\    }} finally {{ _dialectActive.delete(ri); }}
        \\  }}
        \\  function dialectOverlayRecord(i) {{
        \\    let lo = 0, hi = _dialectOverlayCount;
        \\    const base = _dialectOverlaysOff >> 2;
        \\    while (lo < hi) {{ const m = (lo + hi) >>> 1; if (_u32[base + m * 2] < i) lo = m + 1; else hi = m; }}
        \\    return lo < _dialectOverlayCount && _u32[base + lo * 2] === i ? _u32[base + lo * 2 + 1] : NULL;
        \\  }}
        \\  function applyDialectOverlay(i, r) {{
        \\    if (!_hasDialectRecords || r == null || typeof r !== "object") return r;
        \\    const ri = dialectOverlayRecord(i);
        \\    if (ri !== NULL) Object.assign(r, dialectRecord(ri, r.start, r.end, true));
        \\    return r;
        \\  }}
        \\
    , .{ .csize = rt.COMMENT_SIZE, .flag = rt.FLAG_DIALECT_RECORDS, .sub = rt.DIALECT_SUBHEADER_SIZE, .node_size = rt.NODE_SIZE, .overlay_size = rt.DIALECT_OVERLAY_SIZE });
    if (comptime dialect_enabled) try w.print(
        \\  function _validNodeIndex(v) {{ return Number.isInteger(v) && v >= 0 && v < nodeCount; }}
        \\  function _validateDialectSection() {{
        \\    for (const count of [nodeCount, extraCount, spLen, commentCount, attachedCommentCount, diagCount])
        \\      if (!Number.isInteger(count) || count < 0) throw new RangeError("yuku: invalid section count");
        \\    if (!_validNodeIndex(progIdx)) throw new RangeError("yuku: invalid program index");
        \\    const checked = (start, count, size) => {{
        \\      const end = start + count * size;
        \\      if (!Number.isSafeInteger(end) || end < start || end > buffer.byteLength)
        \\        throw new RangeError("yuku: invalid section extent");
        \\      return end;
        \\    }};
        \\    let end = checked({[header]d}, nodeCount, {[node_size]d});
        \\    end = checked(end, extraCount, 4);
        \\    end = checked(end, (spLen + 3) & ~3, 1);
        \\    if (_attached) end = checked(end, nodeCount + 1, 4);
        \\    end = checked(end, attachedCommentCount, {[attached_size]d});
        \\    end = checked(end, commentCount, {[comment_size]d});
        \\    if (end !== _dialectSectionOff) throw new RangeError("yuku: inconsistent dialect offset");
        \\    if (!_hasDialectRecords) {{
        \\      for (let i = 0; i < nodeCount; i++)
        \\        if (_u8[_nodesOff + i * {[node_size]d}] === {[dialect_tag]d}) throw new RangeError("yuku: missing dialect section");
        \\      return;
        \\    }}
        \\    if (!Number.isInteger(_dialectRecordCount) || _dialectRecordCount <= 0 ||
        \\        !Number.isInteger(_dialectOverlayCount) || _dialectOverlayCount < 0)
        \\      throw new RangeError("yuku: non-canonical dialect counts");
        \\    end = checked(_dialectSectionOff, 1, {[sub]d});
        \\    end = checked(end, _dialectRecordCount, {[node_size]d});
        \\    end = checked(end, _dialectOverlayCount, {[overlay_size]d});
        \\    if (end !== dOff) throw new RangeError("yuku: inconsistent dialect extent");
        \\    const schemas = new Array(_dialectRecordCount);
        \\    const poolLimit = _srcLen + spLen;
        \\    for (let ri = 0; ri < _dialectRecordCount; ri++) {{
        \\      const b = (_dialectRecordsOff >> 2) + ri * ({[node_size]d} >> 2);
        \\      const tag = _u32[b] & 255;
        \\      const schema = DIALECT_RECORDS[tag - DIALECT_RECORDS[0].tag];
        \\      if (!schema || schema.tag !== tag) throw new RangeError("yuku: invalid unused dialect tag");
        \\      schemas[ri] = schema;
        \\      for (const field of schema.fields) {{
        \\        const v = _u32[b + field.slot], uv = v >>> 0;
        \\        if ((field.role === "node" || field.role === "host") && !_validNodeIndex(v))
        \\          throw new RangeError("yuku: dialect node role out of bounds");
        \\        if (field.role === "optionalNode" && v !== NULL && !_validNodeIndex(v))
        \\          throw new RangeError("yuku: optional dialect node out of bounds");
        \\        if (field.role === "nodeList") {{
        \\          const len = _u32[b + field.slot + 1] >>> 0;
        \\          if (uv + len > extraCount || uv + len < uv) throw new RangeError("yuku: dialect list out of bounds");
        \\          for (let j = 0; j < len; j++) if (!_validNodeIndex(_u32[_extraBase + uv + j]))
        \\            throw new RangeError("yuku: dialect list node out of bounds");
        \\        }}
        \\        if (field.role === "string") {{
        \\          const e = _u32[b + field.slot + 1] >>> 0;
        \\          if (uv > e || (uv < _srcLen ? e > _srcLen : e > poolLimit))
        \\            throw new RangeError("yuku: dialect string out of bounds");
        \\        }}
        \\      }}
        \\    }}
        \\    for (let i = 0; i < nodeCount; i++) {{
        \\      const b = ({[header]d} >> 2) + i * ({[node_size]d} >> 2);
        \\      if ((_u32[b] & 255) !== {[dialect_tag]d}) continue;
        \\      const ri = _u32[b + {[data_start]d}];
        \\      if (ri < 0 || ri >= _dialectRecordCount || schemas[ri].overlay)
        \\        throw new RangeError("yuku: invalid direct dialect record use");
        \\    }}
        \\    let previousHost = -1;
        \\    const overlayBase = _dialectOverlaysOff >> 2;
        \\    for (let i = 0; i < _dialectOverlayCount; i++) {{
        \\      const host = _u32[overlayBase + i * 2], ri = _u32[overlayBase + i * 2 + 1];
        \\      if (!_validNodeIndex(host) || host <= previousHost || ri < 0 || ri >= _dialectRecordCount || !schemas[ri].overlay)
        \\        throw new RangeError("yuku: invalid dialect overlay");
        \\      const hostField = schemas[ri].fields.find(field => field.role === "host");
        \\      const rb = (_dialectRecordsOff >> 2) + ri * ({[node_size]d} >> 2);
        \\      if (!hostField || _u32[rb + hostField.slot] !== host) throw new RangeError("yuku: overlay host mismatch");
        \\      previousHost = host;
        \\    }}
        \\    const states = new Uint8Array(_dialectRecordCount);
        \\    let visitRecord;
        \\    const visitNodeIndex = value => {{
        \\      if (!_validNodeIndex(value)) throw new RangeError("yuku: dialect cycle node out of bounds");
        \\      const nb = ({[header]d} >> 2) + value * ({[node_size]d} >> 2);
        \\      if ((_u32[nb] & 255) === {[dialect_tag]d}) visitRecord(_u32[nb + {[data_start]d}]);
        \\    }};
        \\    visitRecord = ri => {{
        \\      if (ri < 0 || ri >= _dialectRecordCount) throw new RangeError("yuku: dangling dialect cycle edge");
        \\      if (states[ri] === 1) throw new RangeError("yuku: cyclic dialect record");
        \\      if (states[ri] === 2) return;
        \\      states[ri] = 1;
        \\      const schema = schemas[ri], b = (_dialectRecordsOff >> 2) + ri * ({[node_size]d} >> 2);
        \\      for (const field of schema.fields) {{
        \\        const v = _u32[b + field.slot];
        \\        if (field.role === "node") visitNodeIndex(v);
        \\        else if (field.role === "optionalNode" && v !== NULL) visitNodeIndex(v);
        \\        else if (field.role === "nodeList") {{
        \\          const start = v >>> 0, len = _u32[b + field.slot + 1] >>> 0;
        \\          for (let j = 0; j < len; j++) visitNodeIndex(_u32[_extraBase + start + j]);
        \\        }}
        \\      }}
        \\      states[ri] = 2;
        \\    }};
        \\    for (let ri = 0; ri < _dialectRecordCount; ri++) visitRecord(ri);
        \\  }}
        \\
    , .{
        .header = rt.HEADER_SIZE,
        .node_size = rt.NODE_SIZE,
        .attached_size = rt.ATTACHED_COMMENT_SIZE,
        .comment_size = rt.COMMENT_SIZE,
        .dialect_tag = rt.dialectNodeTag(),
        .sub = rt.DIALECT_SUBHEADER_SIZE,
        .overlay_size = rt.DIALECT_OVERLAY_SIZE,
        .data_start = rt.NODE_HEADER_U32S,
    });
    if (comptime !dialect_enabled) try w.print(
        \\  const dOff = _cOff + commentCount * {[csize]d};
    , .{ .csize = rt.COMMENT_SIZE });
    try w.print(
        \\  function _decodeComments() {{
        \\    const out = new Array(commentCount);
        \\    for (let j = 0; j < commentCount; j++) {{
        \\      const o = (_cOff >> 2) + j * {[c_stride]d};
        \\      const cf = _u32[o] & 255;
        \\      const vs = _u32[o + {[c_vs]d}], ve = _u32[o + {[c_ve]d}];
        \\      const ss = _u32[o + {[c_ss]d}], se = _u32[o + {[c_se]d}];
        \\      out[j] = {{
        \\        type: (cf & 1) ? "Block" : "Line",
        \\        value: str(vs, ve),
        \\        start: _p(ss),
        \\        end: _p(se),
        \\      }};
        \\    }}
        \\    return out;
        \\  }}
        \\  function _decodeDiagnostics() {{
        \\    const out = new Array(diagCount);
        \\    const dv = new DataView(buffer);
        \\    let dp = dOff;
        \\    for (let j = 0; j < diagCount; j++) {{
        \\      const sev = SEVERITY[_u8[dp]]; dp++;
        \\      const ds = _p(dv.getUint32(dp, true)); dp += 4;
        \\      const de = _p(dv.getUint32(dp, true)); dp += 4;
        \\      const ml = dv.getUint32(dp, true); dp += 4;
        \\      const msg = _td.decode(_u8.subarray(dp, dp + ml)); dp += ml;
        \\      const hh = _u8[dp]; dp++;
        \\      let help = null;
        \\      if (hh) {{
        \\        const hl = dv.getUint32(dp, true); dp += 4;
        \\        help = _td.decode(_u8.subarray(dp, dp + hl)); dp += hl;
        \\      }}
        \\      const lc = dv.getUint32(dp, true); dp += 4;
        \\      const labels = new Array(lc);
        \\      for (let k = 0; k < lc; k++) {{
        \\        const ls = _p(dv.getUint32(dp, true)); dp += 4;
        \\        const le = _p(dv.getUint32(dp, true)); dp += 4;
        \\        const lml = dv.getUint32(dp, true); dp += 4;
        \\        labels[k] = {{
        \\          start: ls,
        \\          end: le,
        \\          message: _td.decode(_u8.subarray(dp, dp + lml)),
        \\        }};
        \\        dp += lml;
        \\      }}
        \\      out[j] = {{ severity: sev, message: msg, start: ds, end: de, help, labels }};
        \\    }}
        \\    return out;
        \\  }}
        \\
    , .{
        .c_stride = rt.COMMENT_SIZE / 4,
        .c_vs = rt.COMMENT_VALUE_START_OFFSET / 4,
        .c_ve = rt.COMMENT_VALUE_END_OFFSET / 4,
        .c_ss = rt.COMMENT_SPAN_START_OFFSET / 4,
        .c_se = rt.COMMENT_SPAN_END_OFFSET / 4,
    });

    if (mode == .analyzer) {
        try writeParentBody(w);
        try writeSemanticBody(w);
    }

    if (comptime dialect_enabled) try w.writeAll("  _validateDialectSection();\n");
    try w.writeAll(
        \\  let _program, _diagnostics, _comments;
        \\  return {
        \\    get program() {
        \\      return _program !== undefined ? _program : (_program = node(progIdx));
        \\    },
        \\    get comments() {
        \\      return _comments !== undefined
        \\        ? _comments
        \\        : (_comments = _decodeComments());
        \\    },
        \\    get diagnostics() {
        \\      return _diagnostics !== undefined
        \\        ? _diagnostics
        \\        : (_diagnostics = _decodeDiagnostics());
        \\    },
        \\
    );

    if (mode == .analyzer) try w.writeAll(
        \\    nodeOf: node,
        \\    indexOf: (n) => _nodeIndexes.get(n),
        \\    parentIndex: (i) => _parents()[i],
        \\    startOf, endOf, str,
        \\    get semantic() { return _semantic(); },
        \\
    );

    try w.writeAll(
        \\  };
        \\}
        \\
    );
}

fn writeParentBody(w: *Writer) !void {
    try w.print(
        \\  let _parentArr;
        \\  function _parents() {{
        \\    if (_parentArr !== undefined) return _parentArr;
        \\    const p = new Int32Array(nodeCount).fill(-1);
        \\
    , .{});
    // A dialect node's children live in its record, and an overlay hangs extra
    // children off a host node; CHILD_SLOTS knows neither.
    if (comptime dialect_enabled) try w.print(
        \\    function visitRecord(ri, parent) {{
        \\      const b = (_dialectRecordsOff >> 2) + ri * ({[size]d} >> 2);
        \\      const schema = DIALECT_RECORDS[(_u32[b] & 255) - DIALECT_RECORDS[0].tag];
        \\      for (const field of schema.fields) {{
        \\        const v = _u32[b + field.slot];
        \\        if (field.role === "node" || (field.role === "optionalNode" && v !== NULL)) visit(v, parent);
        \\        else if (field.role === "nodeList") {{
        \\          const len = _u32[b + field.slot + 1];
        \\          for (let j = 0; j < len; j++) visit(_u32[_extraBase + v + j], parent);
        \\        }}
        \\      }}
        \\    }}
        \\
    , .{ .size = rt.NODE_SIZE });
    try w.print(
        \\    function visit(i, parent) {{
        \\      const o = _nodesOff + i * {[size]d};
        \\      const tag = _u8[o];
        \\      const b = o >> 2;
        \\
    , .{ .size = rt.NODE_SIZE });
    if (comptime dialect_enabled) try w.print(
        \\      if (tag === {[dialect_tag]d}) {{ p[i] = parent; visitRecord(_u32[b + {[data_start]d}], i); return; }}
        \\
    , .{ .dialect_tag = rt.dialectNodeTag(), .data_start = rt.NODE_HEADER_U32S });
    try w.print(
        \\      if (IS_NODE[tag]) {{ p[i] = parent; parent = i; }}
        \\      const ops = CHILD_SLOTS[tag];
        \\      for (let q = 0; q < ops.length; q += 2) {{
        \\        const slot = ops[q + 1];
        \\        if (ops[q] === 0) {{
        \\          const c = _u32[b + slot];
        \\          if (c !== NULL) visit(c, parent);
        \\        }} else {{
        \\          const s = _u32[b + slot];
        \\          const len = ops[q] === 1
        \\            ? _u32[b + slot + 1]
        \\            : ops[q] === 2
        \\              ? _u8[o + {[f0]d}] | (_u8[o + {[f01]d}] << 8)
        \\              : _u8[o + {[f0b]d}] | (_u8[o + {[f0b1]d}] << 8);
        \\          for (let j = 0; j < len; j++) {{
        \\            const c = _u32[_extraBase + s + j];
        \\            if (c !== NULL) visit(c, parent);
        \\          }}
        \\        }}
        \\      }}
        \\
    , .{
        .f0 = rt.NODE_FIELD0_OFFSET,
        .f01 = rt.NODE_FIELD0_OFFSET + 1,
        .f0b = rt.NODE_FIELD0B_OFFSET,
        .f0b1 = rt.NODE_FIELD0B_OFFSET + 1,
    });
    if (comptime dialect_enabled) try w.writeAll(
        \\      const ri = dialectOverlayRecord(i);
        \\      if (ri !== NULL) visitRecord(ri, parent);
        \\
    );
    try w.writeAll(
        \\    }
        \\    visit(progIdx, -1);
        \\    return (_parentArr = p);
        \\  }
        \\
    );
}

fn writeSemanticBody(w: *Writer) !void {
    try w.print(
        \\  let _semView;
        \\  function _semantic() {{
        \\    if (_semView !== undefined) return _semView;
        \\    if (!(_flags & {[flag]d})) return (_semView = null);
        \\    let dp = dOff;
        \\    for (let j = 0; j < diagCount; j++) {{
        \\      dp += 9;
        \\      const ml = dv.getUint32(dp, true); dp += 4 + ml;
        \\      if (_u8[dp++]) {{ const hl = dv.getUint32(dp, true); dp += 4 + hl; }}
        \\      const lc = dv.getUint32(dp, true); dp += 4;
        \\      for (let k = 0; k < lc; k++) {{
        \\        dp += 8;
        \\        const lml = dv.getUint32(dp, true); dp += 4 + lml;
        \\      }}
        \\    }}
        \\    let o = ((dp + 3) & ~3) >> 2;
        \\    if (o + {[sub]d} > _u32.length)
        \\      throw new RangeError("yuku-analyzer: truncated semantic sub-header");
        \\    const scopeCount = _u32[o], symbolCount = _u32[o + 1],
        \\          referenceCount = _u32[o + 2], declNodeCount = _u32[o + 3],
        \\          importCount = _u32[o + 4], exportCount = _u32[o + 5],
        \\          nodeScopeCount = _u32[o + 6], moduleFlags = _u32[o + 7];
        \\    o += {[sub]d};
        \\    const scopes = _u32.subarray(o, o + scopeCount * {[scope]d});
        \\    o += scopeCount * {[scope]d};
        \\    const symbols = _u32.subarray(o, o + symbolCount * {[symbol]d});
        \\    o += symbolCount * {[symbol]d};
        \\    const declNodes = _u32.subarray(o, o + declNodeCount);
        \\    o += declNodeCount;
        \\    const references = _u32.subarray(o, o + referenceCount * {[reference]d});
        \\    o += referenceCount * {[reference]d};
        \\    const imports = _u32.subarray(o, o + importCount * {[import]d});
        \\    o += importCount * {[import]d};
        \\    const exports = _u32.subarray(o, o + exportCount * {[expt]d});
        \\    o += exportCount * {[expt]d};
        \\    const nodeScopes = _u32.subarray(o, o + nodeScopeCount);
        \\    o += nodeScopeCount;
        \\    if (o > _u32.length)
        \\      throw new RangeError("yuku-analyzer: truncated semantic sections");
        \\
    , .{
        .flag = rt.FLAG_SEMANTIC,
        .sub = sem_rt.SUBHEADER_SIZE / 4,
        .scope = sem_rt.SCOPE_SIZE / 4,
        .symbol = sem_rt.SYMBOL_SIZE / 4,
        .reference = sem_rt.REFERENCE_SIZE / 4,
        .import = sem_rt.IMPORT_SIZE / 4,
        .expt = sem_rt.EXPORT_SIZE / 4,
    });
    try writeSemanticAccessors(w);
    try w.writeAll(
        \\  }
        \\
    );
}

fn writeSemanticAccessors(w: *Writer) !void {
    const Scope = sem_rt.PackedScope;
    const Sym = sem_rt.PackedSymbol;
    const Ref = sem_rt.PackedReference;
    const Imp = sem_rt.PackedImport;
    const Exp = sem_rt.PackedExport;
    try w.writeAll(
        \\    const _id = (v) => v === NULL ? null : v;
        \\    return (_semView = {
        \\
    );
    try w.print(
        \\      scope: {{
        \\        count: scopeCount,
        \\        kind: (i) => SCOPE_KINDS[{[bits]s} & {[kmask]d}],
        \\        strict: (i) => (({[bits]s} >> {[strict]d}) & 1) !== 0,
        \\        node: (i) => node({[n]s}),
        \\        nodeIndex: (i) => {[n]s},
        \\        parentId: (i) => _id({[p]s}),
        \\        hoistTargetId: (i) => {[h]s},
        \\        start: (i) => startOf({[n]s}),
        \\        end: (i) => endOf({[n]s}),
        \\      }},
        \\
    , .{
        .bits = comptime cell("scopes", Scope, "bits"),
        .kmask = sem_rt.SCOPE_KIND_MASK,
        .strict = sem_rt.SCOPE_STRICT_BIT,
        .n = comptime cell("scopes", Scope, "node"),
        .p = comptime cell("scopes", Scope, "parent"),
        .h = comptime cell("scopes", Scope, "hoist_target"),
    });
    try w.print(
        \\      symbol: {{
        \\        count: symbolCount,
        \\        name: (i) => {[name]s},
        \\        flags: (i) => {[flags]s},
        \\        scopeId: (i) => {[scope]s},
        \\        declCount: (i) => {[dlen]s},
        \\        declNode: (i, j) => node(declNodes[{[dstart]s} + j]),
        \\        declNodeIndex: (i, j) => declNodes[{[dstart]s} + j],
        \\      }},
        \\
    , .{
        .name = comptime strCell("symbols", Sym, "name_start", "name_end"),
        .flags = comptime cell("symbols", Sym, "flags"),
        .scope = comptime cell("symbols", Sym, "scope"),
        .dlen = comptime cell("symbols", Sym, "decls_len"),
        .dstart = comptime cell("symbols", Sym, "decls_start"),
    });
    try w.print(
        \\      reference: {{
        \\        count: referenceCount,
        \\        name: (i) => {[name]s},
        \\        scopeId: (i) => {[scope]s},
        \\        node: (i) => node({[n]s}),
        \\        nodeIndex: (i) => {[n]s},
        \\        space: (i) => REFERENCE_SPACES[({[bits]s} >> {[sshift]d}) & {[smask]d}],
        \\        inTypePosition: (i) => REFERENCE_TYPE_POSITION[({[bits]s} >> {[sshift]d}) & {[smask]d}],
        \\        isWrite: (i) => (({[bits]s} >> {[wbit]d}) & 1) !== 0,
        \\        symbolId: (i) => _id({[sym]s}),
        \\        start: (i) => startOf({[n]s}),
        \\        end: (i) => endOf({[n]s}),
        \\      }},
        \\
    , .{
        .name = comptime strCell("references", Ref, "name_start", "name_end"),
        .scope = comptime cell("references", Ref, "scope"),
        .n = comptime cell("references", Ref, "node"),
        .bits = comptime cell("references", Ref, "bits"),
        .sshift = sem_rt.REFERENCE_SPACE_SHIFT,
        .smask = sem_rt.REFERENCE_SPACE_MASK,
        .wbit = sem_rt.REFERENCE_WRITE_BIT,
        .sym = comptime cell("references", Ref, "symbol"),
    });
    try w.print(
        \\      import: {{
        \\        count: importCount,
        \\        kind: (i) => IMPORT_KINDS[{[bits]s} & {[kmask]d}],
        \\        symbolId: (i) => _id({[sym]s}),
        \\        name: (i) => {[name]s},
        \\        specifier: (i) => {[spec]s},
        \\        typeOnly: (i) => (({[bits]s} >> {[tbit]d}) & 1) !== 0,
        \\        phase: (i) =>
        \\          ({[bits]s} >> {[hpbit]d}) & 1
        \\            ? IMPORT_PHASES[({[bits]s} >> {[pbit]d}) & 1]
        \\            : null,
        \\        node: (i) => node({[n]s}),
        \\      }},
        \\
    , .{
        .sym = comptime cell("imports", Imp, "symbol"),
        .bits = comptime cell("imports", Imp, "bits"),
        .kmask = sem_rt.IMPORT_KIND_MASK,
        .name = comptime strCell("imports", Imp, "name_start", "name_end"),
        .spec = comptime strCell("imports", Imp, "specifier_start", "specifier_end"),
        .tbit = sem_rt.IMPORT_TYPE_BIT,
        .hpbit = sem_rt.IMPORT_HAS_PHASE_BIT,
        .pbit = sem_rt.IMPORT_PHASE_BIT,
        .n = comptime cell("imports", Imp, "node"),
    });
    try w.print(
        \\      export: {{
        \\        count: exportCount,
        \\        kind: (i) => EXPORT_KINDS[{[bits]s} & {[kmask]d}],
        \\        typeOnly: (i) => (({[bits]s} >> {[tbit]d}) & 1) !== 0,
        \\        name: (i) => {[name]s},
        \\        fromName: (i) => {[fname]s},
        \\        specifier: (i) => {[spec]s},
        \\        symbolId: (i) => _id({[sym]s}),
        \\        node: (i) => node({[n]s}),
        \\      }},
        \\      moduleFlags: {{
        \\        usesRequire: (moduleFlags & {[require]d}) !== 0,
        \\        usesModule: (moduleFlags & {[module]d}) !== 0,
        \\        usesExports: (moduleFlags & {[exports]d}) !== 0,
        \\        usesImportMeta: (moduleFlags & {[meta]d}) !== 0,
        \\      }},
        \\      nodeScope: (i) => nodeScopes[i],
        \\    }});
        \\
    , .{
        .bits = comptime cell("exports", Exp, "bits"),
        .kmask = sem_rt.EXPORT_KIND_MASK,
        .tbit = sem_rt.EXPORT_TYPE_BIT,
        .name = comptime strCell("exports", Exp, "name_start", "name_end"),
        .fname = comptime strCell("exports", Exp, "from_name_start", "from_name_end"),
        .spec = comptime strCell("exports", Exp, "specifier_start", "specifier_end"),
        .sym = comptime cell("exports", Exp, "symbol"),
        .n = comptime cell("exports", Exp, "node"),
        .require = @as(u32, 1) << @bitOffsetOf(ModuleFlags, "uses_require"),
        .module = @as(u32, 1) << @bitOffsetOf(ModuleFlags, "uses_module"),
        .exports = @as(u32, 1) << @bitOffsetOf(ModuleFlags, "uses_exports"),
        .meta = @as(u32, 1) << @bitOffsetOf(ModuleFlags, "uses_import_meta"),
    });
}

const TS_FIELDS = [_]struct { node: []const u8, fields: []const []const u8 }{
    .{ .node = "variable_declaration", .fields = &.{"declare"} },
    .{ .node = "variable_declarator", .fields = &.{"definite"} },
    .{ .node = "function", .fields = &.{ "type_parameters", "return_type" } },
    .{ .node = "arrow_function_expression", .fields = &.{ "type_parameters", "return_type" } },
    .{ .node = "class", .fields = &.{
        "type_parameters",
        "super_type_arguments",
        "implements",
        "abstract",
        "declare",
    } },
    .{ .node = "method_definition", .fields = &.{
        "override",
        "optional",
        "abstract",
        "accessibility",
    } },
    .{ .node = "property_definition", .fields = &.{
        "type_annotation",
        "declare",
        "override",
        "optional",
        "definite",
        "readonly",
        "abstract",
        "accessibility",
    } },
    .{ .node = "call_expression", .fields = &.{"type_arguments"} },
    .{ .node = "new_expression", .fields = &.{"type_arguments"} },
    .{ .node = "tagged_template_expression", .fields = &.{"type_arguments"} },
    .{ .node = "jsx_opening_element", .fields = &.{"type_arguments"} },
    .{ .node = "import_declaration", .fields = &.{"import_kind"} },
    .{ .node = "import_specifier", .fields = &.{"import_kind"} },
    .{ .node = "export_named_declaration", .fields = &.{"export_kind"} },
    .{ .node = "export_all_declaration", .fields = &.{"export_kind"} },
    .{ .node = "export_specifier", .fields = &.{"export_kind"} },
    .{ .node = "binding_rest_element", .fields = &.{
        "decorators",
        "optional",
        "type_annotation",
    } },
    .{ .node = "binding_identifier", .fields = &.{ "decorators", "optional", "type_annotation" } },
    .{ .node = "assignment_pattern", .fields = &.{ "decorators", "optional", "type_annotation" } },
    .{ .node = "object_pattern", .fields = &.{ "decorators", "optional", "type_annotation" } },
    .{ .node = "array_pattern", .fields = &.{ "decorators", "optional", "type_annotation" } },
    .{ .node = "ts_index_signature", .fields = &.{"static"} },
};

fn isTsField(comptime tag: []const u8, comptime field: []const u8) bool {
    for (TS_FIELDS) |e| {
        if (!std.mem.eql(u8, e.node, tag)) continue;
        for (e.fields) |f| if (std.mem.eql(u8, f, field)) return true;
    }
    return false;
}

fn hasAnyTsField(comptime tag: []const u8, comptime T: type) bool {
    if (@typeInfo(T) != .@"struct") return false;
    inline for (std.meta.fields(T)) |f| if (comptime isTsField(tag, f.name)) return true;
    return false;
}

const Extra = struct { field: []const u8, value: []const u8 };

const IDENT_EXTRAS = [_]Extra{
    .{ .field = "decorators", .value = "[]" },
    .{ .field = "optional", .value = "false" },
    .{ .field = "typeAnnotation", .value = "null" },
};

const TS_EXTRAS = [_]struct { node: []const u8, extras: []const Extra }{
    .{ .node = "identifier_reference", .extras = &IDENT_EXTRAS },
    .{ .node = "identifier_name", .extras = &IDENT_EXTRAS },
    .{ .node = "label_identifier", .extras = &IDENT_EXTRAS },
    .{ .node = "binding_property", .extras = &.{.{ .field = "optional", .value = "false" }} },
    .{ .node = "object_property", .extras = &.{.{ .field = "optional", .value = "false" }} },
    .{ .node = "expression_statement", .extras = &.{.{ .field = "directive", .value = "null" }} },
    .{ .node = "binding_rest_element", .extras = &.{.{ .field = "value", .value = "null" }} },
    .{ .node = "export_default_declaration", .extras = &.{
        .{ .field = "exportKind", .value = "\"value\"" },
    } },
    .{ .node = "ts_property_signature", .extras = &.{
        .{ .field = "accessibility", .value = "null" },
        .{ .field = "static", .value = "false" },
    } },
    .{ .node = "ts_index_signature", .extras = &.{.{ .field = "accessibility", .value = "null" }} },
    .{ .node = "ts_parameter_property", .extras = &.{
        .{ .field = "static", .value = "false" },
    } },
};

fn tsExtrasOf(comptime name: []const u8) []const Extra {
    inline for (TS_EXTRAS) |e| {
        if (comptime std.mem.eql(u8, e.node, name)) return e.extras;
    }
    return &.{};
}

fn writeTsExtras(w: *Writer, comptime name: []const u8) !void {
    inline for (comptime tsExtrasOf(name)) |e| {
        try w.print("r.{s} = {s}; ", .{ e.field, e.value });
    }
}

fn emit(w: *Writer, comptime fmt: []const u8, args: anytype) !void {
    try w.print(fmt, args);
    try w.writeByte('\n');
}

fn fieldIdx(comptime T: type, comptime name: []const u8) comptime_int {
    return std.meta.fieldIndex(T, name) orelse
        @compileError("field '" ++ name ++ "' not found in " ++ @typeName(T));
}

// u32 slot for f{N} in the generated js, slot 0 is the header f0 so slots 1 and up add 1
fn slotOf(comptime T: type, comptime field: []const u8) u32 {
    return rt.u32SlotForField(T, fieldIdx(T, field)) + 1;
}

fn u32IndexOf(comptime T: type, comptime field: []const u8) u32 {
    return rt.u32SlotForField(T, fieldIdx(T, field)) + rt.NODE_HEADER_U32S;
}

fn flagBit(comptime T: type, comptime field: []const u8) u32 {
    return rt.flagBitForField(T, fieldIdx(T, field));
}

fn flagMask(comptime T: type, comptime field: []const u8) u32 {
    return flagMaskAt(T, fieldIdx(T, field));
}

fn flagMaskAt(comptime T: type, comptime i: usize) u32 {
    return @as(u32, 1) << @intCast(rt.flagBitForField(T, i));
}

fn enumMask(comptime E: type) u32 {
    return (@as(u32, 1) << @intCast(rt.enumBitWidth(E))) - 1;
}

// the entire body of both entry points. gen_parser_decoder.zig and
// gen_analyzer_decoder.zig differ only in the Mode they hand to this
pub fn emitToStdout(init: std.process.Init, mode: Mode) !void {
    var arena = std.heap.ArenaAllocator.init(std.heap.page_allocator);
    defer arena.deinit();
    var generated: std.Io.Writer.Allocating = .init(arena.allocator());
    try generate(&generated.writer, mode);
    var buffer: [64 * 1024]u8 = undefined;
    var output = std.Io.File.stdout().writer(init.io, &buffer);
    try output.interface.writeAll(generated.written());
    try output.flush();
}
